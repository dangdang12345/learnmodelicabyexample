{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 传值\n",
    "\n",
    "在建立子系统模型时，子系统很经常会包含一些会传递到其自身部件的参数。例如，考虑我们在对基本旋转组件的讨论中使用的以下系统模型：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.Examples;\n",
    "model SMD\n",
    "  Components.Damper damper2(d=1);\n",
    "  Components.Ground ground;\n",
    "  Components.Spring spring2(c=5);\n",
    "  Components.Inertia inertia2(J=1,\n",
    "    phi(fixed=true, start=1),\n",
    "    w(fixed=true, start=0));\n",
    "  Components.Damper damper1(d=0.2);\n",
    "  Components.Spring spring1(c=11);\n",
    "  Components.Inertia inertia1(\n",
    "    J=0.4,\n",
    "    phi(fixed=true, start=0),\n",
    "    w(fixed=true, start=0));\n",
    "equation\n",
    "  // ...\n",
    "end SMD;\n",
    "```\n",
    "\n",
    "有时候，我们会想在不同的上下文中使用这个模型，并改变一些部件参数的值，例如d。我们可以让d成为子系统级别的参数，然后利用修改语句将值通过层次结构传递。结果应是如此：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.Examples;\n",
    "model SMD\n",
    "  import Modelica.SIunits.*;\n",
    "  parameter RotationalDampingConstant d;\n",
    "  Components.Damper damper2(d=d);\n",
    "  // ...\n",
    "  ```\n",
    "这里有一个问题。用户可能会去下层改变damper2.d的值，而不在SMD模型修改d参数。要避免d参数和damper2.d参数的不同步（具有不同的值），我们可以使用final限定词将其永久绑定起来：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.Examples;\n",
    "model SMD\n",
    "  import Modelica.SIunits.*;\n",
    "  parameter RotationalDampingConstant d;\n",
    "  Components.Damper damper2(final d=d);\n",
    "  // ...\n",
    "```\n",
    "通过添加**final限定词**，我们表明参数damper2.d的**值不可以再修改**。任何修改必须指向d。\n",
    "用相同方式代替在SMD模型里所有在代码里固定的数值，如下模型会得到很高的可重用性：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.Examples;\n",
    "model SMD\n",
    "  import Modelica.SIunits.*;\n",
    "\n",
    "  parameter RotationalDampingConstant d1, d2;\n",
    "  parameter RotationalSpringConstant c1, c2;\n",
    "  parameter Inertia J1, J2;\n",
    "  parameter Angle phi1_init=0, phi2_init=0;\n",
    "  parameter AngularVelocity w1_init=0, w2_init=0;\n",
    "\n",
    "  Components.Damper damper2(final d=d2);\n",
    "  Components.Ground ground;\n",
    "  Components.Spring spring2(final c=c2);\n",
    "  Components.Inertia inertia2(\n",
    "    final J=J2,\n",
    "    phi(fixed=true, final start=phi2_init),\n",
    "    w(fixed=true, final start=w2_init));\n",
    "  Components.Damper damper1(final d=d1);\n",
    "  Components.Spring spring1(final c=c1);\n",
    "  Components.Inertia inertia1(\n",
    "    final J=J1,\n",
    "    phi(fixed=true, final start=phi1_init),\n",
    "    w(fixed=true, final start=w1_init));\n",
    "equation\n",
    "  // ...\n",
    "end SMD;\n",
    "```\n",
    "如果我们想用某组特定的参数值，有两种可行的方法。一种方法是扩展上述的参数化模型，并在extends声明包括修改语句，如：\n",
    "\n",
    "```Modelica\n",
    "model SpecificSMD\n",
    "  extends SMD(d2=1, c2=5, J2=1,\n",
    "              d1=0.5, c1=11, J1=0.4,\n",
    "              phi1_init=1);\n",
    "```\n",
    "\n",
    "需要注意的是，我们并不需要为phi2_init、w1_init、w2_init添加修改语句，因为这些参数在声明都带有默认值。一般情况下，只有在参数的默认值适用于绝大部分情况时，才应该设定。这样做的原因在于如果参数没有默认值，大多数的Modelica编译器会生成警告，以提醒你必须进行赋值。但是，如果存在默认值，编译器会默默地使用默认值。如果默认值不合理或不典型，那么你会默默地在模型里引入一个不合理的值。\n",
    "\n",
    "另一方面，我们回到关于传值的主题。另一个可用的方法将是实例化SMD模型，并在声明的变量使用修改语句去指定参数值，如：\n",
    "\n",
    "```Modelica\n",
    "SMD mysmd(d2=1, c2=5, J2=1,\n",
    "          d1=0.5, c1=11, J1=0.4,\n",
    "          phi1_init=1);\n",
    "```\n",
    "\n",
    "在下章架构模型之前，我们暂时不会讨论哪种方法更好。"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
