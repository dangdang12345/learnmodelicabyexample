{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 带迁移的掠食者猎物方程\n",
    "\n",
    "在这一节中，我们将再次重温掠食者猎物模型，**目的是了解在前面关于可重用组件模型的工作基础上，这个模型有什么变化**。\n",
    "\n",
    "先**创建各个地理区域的可重用（一般种群动力学）模型，然后，我们再用迁移模型将这些地域连接在一起**。\n",
    "\n",
    "\n",
    "## 双种群的地区\n",
    "\n",
    "所有在本节中的模型都会使用以下包括兔、狐狸种群的模型。而模型使用通常的掠食者猎物人口动态。其Modelica源代码为：\n",
    "```Modelica\n",
    "within ModelicaByExample.Subsystems.LotkaVolterra.Components;\n",
    "model TwoSpecies \"Lotka-Volterra two species configuration\"\n",
    "  // Import several component models from ModelicaByExample.Components.LotkaVolterra\n",
    "  import ModelicaByExample.Components.LotkaVolterra.Components.RegionalPopulation;\n",
    "  import ModelicaByExample.Components.LotkaVolterra.Components.Reproduction;\n",
    "  import ModelicaByExample.Components.LotkaVolterra.Components.Starvation;\n",
    "  import ModelicaByExample.Components.LotkaVolterra.Components.Predation;\n",
    "\n",
    "  parameter Real alpha=0.1 \"Birth rate\";\n",
    "  parameter Real gamma=0.4 \"Starvation coefficient\";\n",
    "  parameter Real initial_rabbit_population=10 \"Initial rabbit population\";\n",
    "  parameter Real initial_fox_population=10 \"Initial fox population\";\n",
    "  parameter Real beta=0.02 \"Prey (rabbits) consumed\";\n",
    "  parameter Real delta=0.02 \"Predators (foxes) fed\";\n",
    "\n",
    "  ModelicaByExample.Components.LotkaVolterra.Interfaces.Species rabbits\n",
    "    \"Population of rabbits in this region\"\n",
    "    annotation (Placement(transformation(extent={{-110,-10},{-90,10}})));\n",
    "  ModelicaByExample.Components.LotkaVolterra.Interfaces.Species foxes\n",
    "    \"Population of foxes in this region\"\n",
    "    annotation (Placement(transformation(extent={{90,-10},{110,10}})));\n",
    "protected\n",
    "  RegionalPopulation rabbit_population(\n",
    "      initial_population=initial_rabbit_population,\n",
    "      init=RegionalPopulation.InitializationOptions.FixedPopulation) \"Rabbit population\"\n",
    "    annotation (Placement(transformation(extent={{-50,-60},{-30,-40}})));\n",
    "  Reproduction reproduction(alpha=alpha) \"Reproduction of rabbits\"\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        origin={-80,-50})));\n",
    "  RegionalPopulation fox_population(\n",
    "      init=RegionalPopulation.InitializationOptions.FixedPopulation,\n",
    "      initial_population=initial_fox_population)\n",
    "    annotation (Placement(transformation(extent={{30,-60},{50,-40}})));\n",
    "  Starvation fox_starvation(gamma=gamma) \"Starvation of foxes\"\n",
    "    annotation (Placement(transformation(extent={{70,-60},{90,-40}})));\n",
    "  Predation fox_predation(beta=beta, delta=delta)\n",
    "    \"Foxes eating rabbits\"\n",
    "    annotation (Placement(transformation(extent={{-10,-30},{10,-10}})));\n",
    "equation\n",
    "  connect(reproduction.species, rabbit_population.species)\n",
    "    annotation (Line(\n",
    "      points={{-80,-40},{-80,-20},{-40,-20},{-40,-40}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(fox_predation.a, rabbit_population.species)\n",
    "    annotation (Line(\n",
    "      points={{-10,-20},{-40,-20},{-40,-40}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(fox_starvation.species, fox_population.species)\n",
    "    annotation (Line(\n",
    "      points={{80,-40},{80,-20},{40,-20},{40,-40}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(fox_population.species, fox_predation.b)\n",
    "    annotation (Line(\n",
    "      points={{40,-40},{40,-20},{10,-20}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(rabbit_population.species, rabbits)\n",
    "    annotation (Line(\n",
    "      points={{-40,-40},{-40,0},{-100,0}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(fox_population.species, foxes)\n",
    "    annotation (Line(\n",
    "      points={{40,-40},{40,0},{100,0}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "end TwoSpecies;\n",
    "```\n",
    "图表此组件呈现为：\n",
    "<img src=\"./figures8/TwoSpecies.svg\" width=800 align=center>\n",
    "\n",
    "此模型将作为本节随后区域人口动态模型的基础。\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 未连接多种群模型\n",
    "\n",
    "本模型由四个不相连的地域组成，对这样的模型其Modelica源代码很简单：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Subsystems.LotkaVolterra.Examples;\n",
    "model UnconnectedPopulations \"Several unconnected regional populations\"\n",
    "  Components.TwoSpecies A \"Region A\"\n",
    "    annotation (Placement(transformation(extent={{-10,80},{10,100}})));\n",
    "  Components.TwoSpecies B \"Region B\"\n",
    "    annotation (Placement(transformation(extent={{-10,20},{10,40}})));\n",
    "  Components.TwoSpecies C \"Region C\"\n",
    "    annotation (Placement(transformation(extent={{-10,-40},{10,-20}})));\n",
    "  Components.TwoSpecies D \"Region D\"\n",
    "    annotation (Placement(transformation(extent={{-10,-100},{10,-80}})));\n",
    "  annotation (experiment(StopTime=40, Intervals=0.008));\n",
    "end UnconnectedPopulations;\n",
    "```\n",
    "<img src=\"./figures8/UnconnectedPopulations.svg\" width=800 align=center>\n",
    "\n",
    "此模型的简图也同样简单,如果我们对该模型进行仿真，每个种群应遵循同一轨迹。因为各个种群的初始条件均是相同的。下面的图显示了这确实如此：\n",
    "\n",
    "<img src=\"./figures8/Uncon.png\" width=400 align=center>\n",
    "\n",
    "\n",
    "稍后我们将观察迁移的影响。但为了完全鉴别迁移的效果，我们需要对不同地域的发展趋势引进一定差异。所以，让我们修改UnconnectedPopulations模型的初始条件以期引进一些区域性的差异：\n",
    "```Modelica\n",
    "within ModelicaByExample.Subsystems.LotkaVolterra.Examples;\n",
    "model InitiallyDifferent \"Multiple regions with different initial populations\"\n",
    "  extends UnconnectedPopulations(\n",
    "    B(initial_rabbit_population=5, initial_fox_population=12),\n",
    "    C(initial_rabbit_population=12, initial_fox_population=5),\n",
    "    D(initial_rabbit_population=7, initial_fox_population=7));\n",
    "end InitiallyDifferent;\n",
    "```\n",
    "\n",
    "如果我们对该模型进行仿真，会看到各区域的种群动态有些许的差异。\n",
    "<img src=\"./figures8/ID.png\" width=600 align=center>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 迁移\n",
    "\n",
    "现在我们已经模拟非相连区域里种群动态。下一步要研究的是迁移对这些动态可能造成的影响。\n",
    "\n",
    "请考虑以下的Modelica迁移模型\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Subsystems.LotkaVolterra.Components;\n",
    "model Migration \"Simple 'diffusion' based model of migration\"\n",
    "  parameter Real rabbit_migration=0.001 \"Rabbit migration rate\";\n",
    "  parameter Real fox_migration=0.005 \"Fox migration rate\";\n",
    "  ModelicaByExample.Components.LotkaVolterra.Interfaces.Species rabbit_a\n",
    "    \"Rabbit population in Region A\"\n",
    "    annotation (Placement(transformation(extent={{-70,90},{-50,110}})));\n",
    "  ModelicaByExample.Components.LotkaVolterra.Interfaces.Species rabbit_b\n",
    "    \"Rabbit population in Region B\"\n",
    "    annotation (Placement(transformation(extent={{-70,-110},{-50,-90}})));\n",
    "  ModelicaByExample.Components.LotkaVolterra.Interfaces.Species fox_a\n",
    "    \"Fox population in Region A\"\n",
    "    annotation (Placement(transformation(extent={{50,90},{70,110}})));\n",
    "  ModelicaByExample.Components.LotkaVolterra.Interfaces.Species fox_b\n",
    "    \"Fox population in Region B\"\n",
    "    annotation (Placement(transformation(extent={{50,-110},{70,-90}})));\n",
    "equation\n",
    "  rabbit_a.rate = (rabbit_a.population-rabbit_b.population)*rabbit_migration;\n",
    "  rabbit_a.rate + rabbit_b.rate = 0 \"Conservation of rabbits\";\n",
    "  fox_a.rate = (fox_a.population-fox_b.population)*fox_migration;\n",
    "  fox_a.rate + fox_b.rate = 0 \"Conservation of foxes\";\n",
    "  annotation ( Icon(graphics={\n",
    "        Rectangle(\n",
    "          extent={{-100,100},{100,-100}},\n",
    "          lineColor={0,127,0},\n",
    "          fillColor={255,255,255},\n",
    "          fillPattern=FillPattern.Solid),\n",
    "        Text(\n",
    "          extent={{-100,20},{100,-20}},\n",
    "          lineColor={0,127,0},\n",
    "          fillColor={255,255,255},\n",
    "          fillPattern=FillPattern.Solid,\n",
    "          textString=\"%name\",\n",
    "          origin={-120,0},\n",
    "          rotation=90),\n",
    "        Bitmap(extent={{-84,82},{-36,-82}}, fileName=\n",
    "              \"modelica://ModelicaByExample/Resources/Images/rabbit.png\"),\n",
    "        Bitmap(extent={{18,80},{94,-84}}, fileName=\n",
    "              \"modelica://ModelicaByExample/Resources/Images/fox.png\")}));\n",
    "end Migration;\n",
    "```\n",
    "\n",
    "此模型利用两个连接区域内兔子和狐狸各自种群大小的区域差去求解迁移的速率。迁移的速率与各自种群大小在区域间之差成正比。换句话说，如果某区域内兔子数比另一区的多，兔子将从人口较多的区域移动到人口较少的区。\n",
    "\n",
    "这实际上是一个“扩散”迁移模型，不一定有生态学上的基础。引入模型的目的是提供一个例子去说明，要如何在各区域现有动态的基础上引入新的效应，以影响区域间的种群动态。\n",
    "\n",
    "如果我们将先前未连接地区用迁移路径连接起来，如：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Subsystems.LotkaVolterra.Examples;\n",
    "model WithMigration \"Connect populations by migration\"\n",
    "  extends InitiallyDifferent;\n",
    "  Components.Migration migrate_AB \"Migration from region A to region B\"\n",
    "    annotation (Placement(transformation(extent={{-10,50},{10,70}})));\n",
    "  Components.Migration migrate_BC \"Migration from region B to region C\"\n",
    "    annotation (Placement(transformation(extent={{-10,-10},{10,10}})));\n",
    "  Components.Migration migrate_CD \"Migration from region C to region D\"\n",
    "    annotation (Placement(transformation(extent={{-10,-70},{10,-50}})));\n",
    "equation\n",
    "  connect(migrate_CD.rabbit_b, D.rabbits) annotation (Line(\n",
    "      points={{-6,-70},{-6,-76},{-20,-76},{-20,-90},{-10,-90}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(migrate_CD.rabbit_a, C.rabbits) annotation (Line(\n",
    "      points={{-6,-50},{-6,-44},{-20,-44},{-20,-30},{-10,-30}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(migrate_BC.rabbit_b, C.rabbits) annotation (Line(\n",
    "      points={{-6,-10},{-6,-16},{-20,-16},{-20,-30},{-10,-30}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(migrate_CD.fox_b, D.foxes) annotation (Line(\n",
    "      points={{6,-70},{6,-76},{20,-76},{20,-90},{10,-90}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(migrate_BC.fox_b, C.foxes) annotation (Line(\n",
    "      points={{6,-10},{6,-16},{20,-16},{20,-30},{10,-30}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(migrate_CD.fox_a, C.foxes) annotation (Line(\n",
    "      points={{6,-50},{6,-44},{20,-44},{20,-30},{10,-30}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(migrate_BC.fox_a, B.foxes) annotation (Line(\n",
    "      points={{6,10},{6,16},{20,16},{20,30},{10,30}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(migrate_BC.rabbit_a, B.rabbits) annotation (Line(\n",
    "      points={{-6,10},{-6,16},{-20,16},{-20,30},{-10,30}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(migrate_AB.fox_b, B.foxes) annotation (Line(\n",
    "      points={{6,50},{6,50},{6,44},{20,44},{20,30},{10,30}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(migrate_AB.rabbit_b, B.rabbits) annotation (Line(\n",
    "      points={{-6,50},{-6,44},{-20,44},{-20,30},{-10,30}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(migrate_AB.fox_a, A.foxes) annotation (Line(\n",
    "      points={{6,70},{6,76},{20,76},{20,90},{10,90}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(migrate_AB.rabbit_a, A.rabbits) annotation (Line(\n",
    "      points={{-6,70},{-6,76},{-20,76},{-20,90},{-10,90}},\n",
    "      color={0,127,0},\n",
    "      smooth=Smooth.None));\n",
    "end WithMigration;\n",
    "```\n",
    "\n",
    "系统简图变为：\n",
    "\n",
    "<img src=\"./figures8/WithMigration.svg\" width=600 align=center>\n",
    "\n",
    "模拟这个系统后，我们可以看到不同区域内的人口动态在开始时并不同步。但是，最终不同的区域都会稳定成重复的行为模式：\n",
    "\n",
    "<img src=\"./figures8/WM.png\" width=600 align=center>\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 结论\n",
    "\n",
    "此前，我们把掠食者猎物方程化为代表捕食、饥饿和繁殖的组件。在本节中，我们能使用这些组件模型建立子系统模型，以表示在特定区域内的种群动态。在此基础上，我们将这些子系统连接为带层级的系统模型，以描述这些不同区域之间迁移的影响。"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
