{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 空间分布的传热\n",
    "\n",
    "下一个创建可重用子系统的例子会有些变化。在这一节中，我们不仅会与本章前面例子里一样，展示如何创建可重用的子系统模型。而且子系统模型将使用部件实例组成的数组。该数组的大小可被用于控制结果的空间分辨率。\n",
    "\n",
    "## 无层级系统\n",
    "\n",
    "从如下所示无层级系统级模型开始：我们有3个热电容和5个热导体，代表的被等分为3段的杆、分段之间的热传导以及每个分段与环境的热传导。\n",
    "\n",
    "<img src=\"./figures8/FlatRod.svg\" width=1600 align=center>\n",
    "\n",
    "模型由热容以及热容之间的热导体组成。这里有3个热容以及5个热导体。热量施加在该系统的左端。系统右端的温度传感器测量最右热容的温度变化。\n",
    "\n",
    "模型的Modelica实现如下：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Subsystems.HeatTransfer.Examples;\n",
    "model FlatRod \"Modeling a heat transfer in a rod in a without subsystems\"\n",
    "  Modelica.Thermal.HeatTransfer.Sources.PrescribedHeatFlow heating\n",
    "    \"Heating actuator\"\n",
    "    annotation (Placement(transformation(extent={{-60,50},{-40,70}})));\n",
    "  Modelica.Blocks.Sources.Step bc(height=10, startTime=0.5) \"Heat profile\"\n",
    "    annotation (Placement(transformation(extent={{-90,50},{-70,70}})));\n",
    "  Modelica.Thermal.HeatTransfer.Components.HeatCapacitor C1(C=0.1, T(fixed=true))\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        origin={-60,2})));\n",
    "  Modelica.Thermal.HeatTransfer.Components.ThermalConductor G1(G=1.2)\n",
    "    annotation (Placement(transformation(extent={{-40,-30},{-20,-10}})));\n",
    "  Modelica.Thermal.HeatTransfer.Components.HeatCapacitor C2(C=0.1, T(fixed=true))\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        origin={0,2})));\n",
    "  Modelica.Thermal.HeatTransfer.Components.ThermalConductor G2(G=1.2)\n",
    "    annotation (Placement(transformation(extent={{20,-30},{40,-10}})));\n",
    "  Modelica.Thermal.HeatTransfer.Components.HeatCapacitor C3(C=0.1, T(fixed=true))\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        origin={60,2})));\n",
    "  Modelica.Thermal.HeatTransfer.Sensors.TemperatureSensor sensor\n",
    "    annotation (Placement(transformation(extent={{80,-30},{100,-10}})));\n",
    "  Modelica.Thermal.HeatTransfer.Components.ThermalConductor wall1(G=0.9)\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=90, origin={-60,-40})));\n",
    "  Modelica.Thermal.HeatTransfer.Sources.FixedTemperature ambient(T=293.15)\n",
    "    \"Ambient temperature\" annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=90, origin={0,-80})));\n",
    "  Modelica.Thermal.HeatTransfer.Components.ThermalConductor wall2(G=0.9)\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=90, origin={0,-40})));\n",
    "  Modelica.Thermal.HeatTransfer.Components.ThermalConductor wall3(G=0.9)\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=90, origin={60,-40})));\n",
    "equation\n",
    "  connect(bc.y, heating.Q_flow) annotation (Line(\n",
    "      points={{-69,60},{-60,60}},\n",
    "      color={0,0,127}, smooth=Smooth.None));\n",
    "  connect(C1.port, G1.port_a) annotation (Line(\n",
    "      points={{-60,-8},{-60,-20},{-40,-20}},\n",
    "      color={191,0,0}, smooth=Smooth.None));\n",
    "  connect(C2.port, G2.port_a) annotation (Line(\n",
    "      points={{0,-8},{0,-20},{20,-20}},\n",
    "      color={191,0,0}, smooth=Smooth.None));\n",
    "  connect(G2.port_b, C3.port) annotation (Line(\n",
    "      points={{40,-20},{60,-20},{60,-8}},\n",
    "      color={191,0,0}, smooth=Smooth.None));\n",
    "  connect(G1.port_b, C2.port) annotation (Line(\n",
    "      points={{-20,-20},{0,-20},{0,-8}},\n",
    "      color={191,0,0}, smooth=Smooth.None));\n",
    "  connect(heating.port, C1.port) annotation (Line(\n",
    "      points={{-40,60},{-30,60},{-30,20},{-80,20},{-80,-20},{-60,-20},{-60,-8}},\n",
    "      color={191,0,0}, smooth=Smooth.None));\n",
    "  connect(wall1.port_b, C1.port) annotation (Line(\n",
    "      points={{-60,-30},{-60,-8}},\n",
    "      color={191,0,0}, smooth=Smooth.None));\n",
    "  connect(wall1.port_a, ambient.port) annotation (Line(\n",
    "      points={{-60,-50},{-60,-60},{0,-60},{0,-70}},\n",
    "      color={191,0,0}, smooth=Smooth.None));\n",
    "  connect(wall2.port_a, ambient.port) annotation (Line(\n",
    "      points={{0,-50},{0,-50},{0,-70}},\n",
    "      color={191,0,0}, smooth=Smooth.None));\n",
    "  connect(wall3.port_a, ambient.port) annotation (Line(\n",
    "      points={{60,-50},{60,-60},{0,-60},{0,-70}},\n",
    "      color={191,0,0}, smooth=Smooth.None));\n",
    "  connect(wall3.port_b, C3.port) annotation (Line(\n",
    "      points={{60,-30},{60,-8}},\n",
    "      color={191,0,0}, smooth=Smooth.None));\n",
    "  connect(sensor.port, C3.port) annotation (Line(\n",
    "      points={{80,-20},{60,-20},{60,-8}},\n",
    "      color={191,0,0}, smooth=Smooth.None));\n",
    "  connect(C2.port, wall2.port_b) annotation (Line(\n",
    "      points={{0,-8},{0,-19},{0,-30},{0,-30}},\n",
    "      color={191,0,0},\n",
    "      smooth=Smooth.None));\n",
    "end FlatRod;\n",
    "```\n",
    "模拟这个系统，我们可以从下面的图内看到最右边热容的温度响应：\n",
    "\n",
    "<img src=\"./figures8/FR.png\" width=500 align=center>\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 分段杆子系统\n",
    "\n",
    "从理论上讲，我们可以把杆分为$N$段。然后，这些分段间有$N−1$条热传导路径。而分段和环境之间则有$N$条热传导路径。前面例子中配置为$N=3$。但我们可以创建一个以$N$为参数的子系统模型。要做到这一点，我们不能简单地将热容拖放到模型中，然后将其与热导体连接起来。因为我们不知道系统确切等分为几份。\n",
    "\n",
    "相反，我们将使用组件数组来代表这一系列热容和热导体。所得到的Rod模型可以写为如下的Modelica代码：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Subsystems.HeatTransfer.Components;\n",
    "model Rod \"Modeling discretized rod\"\n",
    "  import HTC=Modelica.Thermal.HeatTransfer.Components;\n",
    "\n",
    "  parameter Integer n(start=2,min=2) \"Number of rod segments\";\n",
    "  parameter Modelica.SIunits.Temperature T0 \"Initial rod temperature\";\n",
    "  Modelica.Thermal.HeatTransfer.Interfaces.HeatPort_a port_a\n",
    "    \"Thermal connector to ambient\"\n",
    "    annotation (Placement(transformation(extent={{-110,-10},{-90,10}})));\n",
    "  Modelica.Thermal.HeatTransfer.Interfaces.HeatPort_b port_b\n",
    "    \"Thermal connector for rod end 'b'\"\n",
    "    annotation (Placement(transformation(extent={{90,-10},{110,10}})));\n",
    "  parameter Modelica.SIunits.HeatCapacity C\n",
    "    \"Total heat capacity of element (= cp*m)\";\n",
    "  parameter Modelica.SIunits.ThermalConductance G_wall\n",
    "    \"Thermal conductivity of wall\";\n",
    "  parameter Modelica.SIunits.ThermalConductance G_rod\n",
    "    \"Thermal conductivity of rod\";\n",
    "  Modelica.Thermal.HeatTransfer.Interfaces.HeatPort_a ambient\n",
    "    \"Thermal connector for rod end 'a'\"\n",
    "    annotation (Placement(transformation(extent={{-10,-110},{10,-90}})));\n",
    "protected\n",
    "  HTC.HeatCapacitor capacitance[n](each final C=C/n, each T(start=T0, fixed=true))\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        origin={-30,20})));\n",
    "  HTC.ThermalConductor wall[n](each final G=G_wall/n)\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=90, origin={-30,-20})));\n",
    "  HTC.ThermalConductor rod_conduction[n-1](each final G=G_rod)\n",
    "    annotation (Placement(transformation(extent={{-10,-10},{10,10}})));\n",
    "equation\n",
    "  for i in 1:n loop\n",
    "    connect(capacitance[i].port, wall[i].port_b) \"Capacitance to walls\";\n",
    "    connect(wall[i].port_a, ambient) \"Walls to ambient\";\n",
    "  end for;\n",
    "  for i in 1:n-1 loop\n",
    "    connect(capacitance[i].port, rod_conduction[i].port_a)\n",
    "      \"Capacitance to next conduction\";\n",
    "    connect(capacitance[i+1].port, rod_conduction[i].port_b)\n",
    "      \"Capacitance to prev conduction\";\n",
    "  end for;\n",
    "  connect(capacitance[1].port, port_a) \"First capacitance to rod end\";\n",
    "  connect(capacitance[n].port, port_b) \"Last capacitance to (other) rod end\";\n",
    "end Rod;\n",
    "```\n",
    "此模型有几个有趣的地方可以注意。首先，杆的等分数目由参数n表示：\n",
    "\n",
    "```Modelica\n",
    "parameter Integer n(start=2, min=2) \"Number of rod segments\";\n",
    "```\n",
    "参数$n$然后用在下列声明内。参数的目的是指定在杆内热容和热导元素的数目：\n",
    "\n",
    "```Modelica\n",
    "HTC.HeatCapacitor capacitance[n](each final C=C/n, each T(start=T0, fixed=true))\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        origin={-30,20})));\n",
    "  HTC.ThermalConductor wall[n](each final G=G_wall/n)\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=90, origin={-30,-20})));\n",
    "  HTC.ThermalConductor rod_conduction[n-1](each final G=G_rod)\n",
    "    annotation (Placement(transformation(extent={{-10,-10},{10,10}})));\n",
    "```\n",
    "\n",
    "需要注意的是，如果我们想对组件数组的每个组件进行修改，如加入G=G_rod，我们可以在修改上使用each限定词。我们将在本章节后面修改语句一节里讨论each限定词以及如何让修改应用于元件数组。\n",
    "\n",
    "现在，我们已经声明了组件数组。然后，我们就可以使用for循环在equation段把热容和热导连接起来：\n",
    "\n",
    "```Modelica\n",
    " for i in 1:n loop\n",
    "    connect(capacitance[i].port, wall[i].port_b) \"Capacitance to walls\";\n",
    "    connect(wall[i].port_a, ambient) \"Walls to ambient\";\n",
    "  end for;\n",
    "  for i in 1:n-1 loop\n",
    "    connect(capacitance[i].port, rod_conduction[i].port_a)\n",
    "      \"Capacitance to next conduction\";\n",
    "    connect(capacitance[i+1].port, rod_conduction[i].port_b)\n",
    "      \"Capacitance to prev conduction\";\n",
    "  end for;\n",
    "  ```\n",
    "  \n",
    "  我们还需要将杆的端部连接到外部连接器，使该杆可和其他模型相连接：\n",
    "```Modelica\n",
    "  connect(capacitance[1].port, port_a) \"First capacitance to rod end\";\n",
    "  connect(capacitance[n].port, port_b) \"Last capacitance to (other) rod end\";\n",
    "```\n",
    "这样，我们就能够创建任意次等分的分段杆模型。\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 空间分辨率\n",
    "\n",
    "现在，我们为Rod模型设定了参数。大家可以看一下分段数量如何影响系统的响应。最终，我们应该看到的是，随着段数增大（或者每段变得更小），解会收敛。\n",
    "\n",
    "首先，考虑n=3的模型，即：\n",
    "```Modelica\n",
    "within ModelicaByExample.Subsystems.HeatTransfer.Examples;\n",
    "model ThreeSegmentRod \"Modeling a heat transfer using 3 segment rod subsystem\"\n",
    "  Modelica.Thermal.HeatTransfer.Sources.PrescribedHeatFlow heating\n",
    "    \"Heating actuator\"\n",
    "    annotation (Placement(transformation(extent={{-60,50},{-40,70}})));\n",
    "  Modelica.Blocks.Sources.Step bc(height=10, startTime=0.5) \"Heat profile\"\n",
    "    annotation (Placement(transformation(extent={{-90,50},{-70,70}})));\n",
    "  Modelica.Thermal.HeatTransfer.Sensors.TemperatureSensor sensor\n",
    "    annotation (Placement(transformation(extent={{80,-10},{100,10}})));\n",
    "  Modelica.Thermal.HeatTransfer.Sources.FixedTemperature ambient(T=293.15)\n",
    "    \"Ambient temperature\" annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=90, origin={0,-80})));\n",
    "  Components.Rod rod(n=3, C=0.3, G_wall=2.7, T0=293.15, G_rod=1.2)\n",
    "    annotation (Placement(transformation(extent={{-20,-20},{20,20}})));\n",
    "equation\n",
    "  connect(bc.y, heating.Q_flow) annotation (Line(\n",
    "      points={{-69,60},{-60,60}},\n",
    "      color={0,0,127}, smooth=Smooth.None));\n",
    "  connect(heating.port, rod.port_a) annotation (Line(\n",
    "      points={{-40,60},{-30,60},{-30,0},{-20,0}},\n",
    "      color={191,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(rod.ambient, ambient.port) annotation (Line(\n",
    "      points={{0,-20},{0,-70},{0,-70}},\n",
    "      color={191,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(rod.port_b, sensor.port) annotation (Line(\n",
    "      points={{20,0},{80,0}},\n",
    "      color={191,0,0},\n",
    "      smooth=Smooth.None));\n",
    "end ThreeSegmentRod;\n",
    "```\n",
    "\n",
    "然后，我们可以从这个模型扩展得到具有更多分段的模型，如：\n",
    "```Modelica\n",
    "within ModelicaByExample.Subsystems.HeatTransfer.Examples;\n",
    "model SixSegmentRod \"Rod divided into 6 pieces\"\n",
    "  extends ThreeSegmentRod(rod(n=6));\n",
    "end SixSegmentRod;\n",
    "within ModelicaByExample.Subsystems.HeatTransfer.Examples;\n",
    "model TenSegmentRod\n",
    "  extends SixSegmentRod(rod(n=10));\n",
    "end TenSegmentRod;\n",
    "within ModelicaByExample.Subsystems.HeatTransfer.Examples;\n",
    "model OneHundredSegmentRod \"Rod divided into 100 pieces\"\n",
    "  extends ThreeSegmentRod(rod(n=100));\n",
    "end OneHundredSegmentRod;\n",
    "within ModelicaByExample.Subsystems.HeatTransfer.Examples;\n",
    "model TwoHundredSegmentRod\n",
    "  extends OneHundredSegmentRod(rod(n=200));\n",
    "end TwoHundredSegmentRod;\n",
    "```\n",
    "\n",
    "如果我们模拟所有这些情况，我们会看到随着n变大，结果似乎收敛到一个共同的解n=10似乎提供了足够精确的解，而不需要引进太多多余的组件：\n",
    "\n",
    "<img src=\"./figures8/SegC.png\" width=500 align=center>\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 结论\n",
    "\n",
    "在这一节中，我们已经看到了要如何使用组件数组建立任意大小的组件，并使用for循环将其连接在一起。"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
