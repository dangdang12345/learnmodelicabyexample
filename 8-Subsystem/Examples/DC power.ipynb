{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 直流电源\n",
    "\n",
    "在这一节中，我们将考虑如何在Modelica实现直流电源模型。我们将再一次展示，怎么重构无层级的系统模型，使得可重用的子系统模型能得到利用。\n",
    "\n",
    "## 无层级电源模型\n",
    "\n",
    "在这里，我们的无层级系统模型是如下的直流电源电路：\n",
    "\n",
    "<img src=\"./figures8/FlatCircuit.svg\" width=800 align=center>\n",
    "\n",
    "\n",
    "模型的Modelica实现则如下：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Subsystems.PowerSupply.Examples;\n",
    "model FlatCircuit \"A model with power source, AC-DC conversion and load in one diagram\"\n",
    "  import Modelica.Electrical.Analog;\n",
    "  Analog.Sources.SineVoltage wall_voltage(V=120, freqHz=60)\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=270, origin={-90,0})));\n",
    "  Analog.Ideal.IdealClosingSwitch switch(Goff=0)\n",
    "    annotation (Placement(transformation(extent={{-80,30},{-60,50}})));\n",
    "  Analog.Ideal.IdealTransformer transformer(Lm1=1, n=10, considerMagnetization=false)\n",
    "    annotation (Placement(transformation(extent={{-50,0},{-30,20}})));\n",
    "  Analog.Ideal.IdealDiode D1(Vkne0e=0, Ron=1e-5, Goff=1e-5)\n",
    "    annotation (Placement(\n",
    "        transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=45, origin={-12,20})));\n",
    "  Analog.Basic.Capacitor capacitor(C=1e-2)\n",
    "    annotation (Placement(\n",
    "        transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=270, origin={60,-10})));\n",
    "  Analog.Basic.Resistor load(R=100)\n",
    "    annotation (Placement(\n",
    "        transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=270, origin={100,-10})));\n",
    "  Modelica.Blocks.Sources.BooleanStep step(startTime=0.25)\n",
    "    annotation (Placement(transformation(extent={{-100,50},{-80,70}})));\n",
    "  Analog.Ideal.IdealDiode D2(Vknee=0, Ron=1e-5, Goff=1e-5)\n",
    "    annotation (Placement(\n",
    "        transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=-45, origin={12,20})));\n",
    "  Analog.Ideal.IdealDiode D3(Vknee=0, Ron=1e-5, Goff=1e-5)\n",
    "    annotation (Placement(\n",
    "        transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=-45, origin={-12,0})));\n",
    "  Analog.Ideal.IdealDiode D4(Vknee=0, Ron=1e-5, Goff=1e-5)\n",
    "    annotation (Placement(\n",
    "        transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=45, origin={12,0})));\n",
    "  Analog.Basic.Ground ground1\n",
    "    annotation (Placement(transformation(extent={{-100,-52},{-80,-32}})));\n",
    "equation\n",
    "  connect(switch.p, wall_voltage.p) annotation (Line(\n",
    "      points={{-80,40},{-90,40},{-90,10}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(switch.n, transformer.p1) annotation (Line(\n",
    "      points={{-60,40},{-50,40},{-50,15}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(step.y, switch.control) annotation (Line(\n",
    "      points={{-79,60},{-70,60},{-70,47}},\n",
    "      color={255,0,255}, smooth=Smooth.None));\n",
    "  connect(D3.n, D4.p) annotation (Line(\n",
    "      points={{-4.92893,-7.07107},{-2.46446,-7.07107},{-2.46446,-7.07107},{\n",
    "          1.09406e-006,-7.07107},{1.09406e-006,-7.07107},{4.92893,-7.07107}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(D1.n, D2.p) annotation (Line(\n",
    "      points={{-4.92893,27.0711},{2.18813e-006,28},{4.92893,28},{4.92893, 27.0711}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(D1.p, D3.p) annotation (Line(\n",
    "      points={{-19.0711,12.9289},{-20,10},{-19.0711,7.07107}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(D2.n, D4.n) annotation (Line(\n",
    "      points={{19.0711,12.9289},{19.0711,11.4644},{19.0711,11.4644},{19.0711,10},\n",
    "          {19.0711,7.07107},{19.0711,7.07107}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(transformer.p2, D1.n) annotation (Line(\n",
    "      points={{-30,15},{-30,34},{0,34},{0,27.0711},{-4.92893,27.0711}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(D4.p, transformer.n2) annotation (Line(\n",
    "      points={{4.92893,-7.07107},{0,-7.07107},{0,-20},{-30,-20},{-30,5}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(wall_voltage.n, transformer.n1) annotation (Line(\n",
    "      points={{-90,-10},{-90,-20},{-50,-20},{-50,5}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(wall_voltage.n, ground1.p) annotation (Line(\n",
    "      points={{-90,-10},{-90,-32}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(transformer.n2, ground1.p) annotation (Line(\n",
    "      points={{-30,5},{-30,-32},{-90,-32}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(load.p, D2.n) annotation (Line(\n",
    "      points={{100,0},{100,12},{20,12},{20,12.9289},{19.0711,12.9289}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(load.p, capacitor.p) annotation (Line(\n",
    "      points={{100,0},{100,12},{60,12},{60,0}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(D1.p, capacitor.n) annotation (Line(\n",
    "      points={{-19.0711,12.9289},{-24,12.9289},{-24,-32},{60,-32},{60,-20}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "  connect(load.n, capacitor.n) annotation (Line(\n",
    "      points={{100,-20},{100,-32},{60,-32},{60,-20}},\n",
    "      color={0,0,255}, smooth=Smooth.None));\n",
    "end FlatCircuit;\n",
    "```\n",
    "电源把交流输入电压（120V，60Hz）经过整流后传入低通滤波器。对模型进行仿真后，我们可以看到load电阻上的电压如下：\n",
    "\n",
    "\n",
    "<img src=\"./figures8/FC.png\" width=1600 align=center>\n",
    "\n",
    "请注意，我们这里的目标是12伏的输出电压。但是，输出信号的质量会随着电源负载的增大而变差。在这个模拟里，负载最初为零（由于连接到电源的负载开关为开路） 。但是，当开关闭合，电流开始流过负载（名为load的电阻）时，我们会开始看到一些错误。"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 有层级电源\n",
    "\n",
    "首先以无层级版本为基础将一部分组件收集、整理成子系统模型，以简化系统模型。我们的系统级电路就变成了：\n",
    "\n",
    "<img src=\"./figures8/SubsystemCircuit.svg\" width=600 align=center>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "上述系统模型采用了BasicPowerSupply模型。此子模型的简图如下所示：\n",
    "\n",
    "<img src=\"./figures8/BasicPowerSupply.svg\" width=600 align=center>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "此可重用电源子系统模型的Modelica源代码为：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Subsystems.PowerSupply.Components;\n",
    "model BasicPowerSupply \"Power supply with transformer and rectifier\"\n",
    "  import Modelica.Electrical.Analog;\n",
    "  parameter Modelica.SIunits.Capacitance C\n",
    "    \"Filter capacitance\"\n",
    "    annotation(Dialog(group=\"General\"));\n",
    "  parameter Modelica.SIunits.Conductance Goff=1.E-5\n",
    "    \"Backward state-off conductance (opened diode conductance)\"\n",
    "    annotation(Dialog(group=\"General\"));\n",
    "  parameter Modelica.SIunits.Resistance Ron=1.E-5\n",
    "    \"Forward state-on differential resistance (closed diode resistance)\"\n",
    "    annotation(Dialog(group=\"General\"));\n",
    "  parameter Real n\n",
    "    \"Turns ratio primary:secondary voltage\"\n",
    "    annotation(Dialog(group=\"Transformer\"));\n",
    "  parameter Boolean considerMagnetization=false\n",
    "    \"Choice of considering magnetization\"\n",
    "    annotation(Dialog(group=\"Transformer\"));\n",
    "  parameter Modelica.SIunits.Inductance Lm1\n",
    "    \"Magnetization inductance w.r.t. primary side\"\n",
    "    annotation(Dialog(group=\"Transformer\", enable=considerMagnetization));\n",
    "\n",
    "  Analog.Interfaces.NegativePin gnd\n",
    "    \"Pin to ground power supply\"\n",
    "    annotation (Placement(transformation(extent={{-110,-70},{-90,-50}})));\n",
    "  Analog.Interfaces.PositivePin p\n",
    "    \"Positive pin on supply side\"\n",
    "    annotation (Placement(transformation(extent={{-110,50},{-90,70}})));\n",
    "  Analog.Interfaces.PositivePin p_load\n",
    "    \"Positive pin for load side\"\n",
    "    annotation (Placement(transformation(extent={{90,50},{110,70}})));\n",
    "  Analog.Interfaces.NegativePin n_load\n",
    "    \"Negative pin for load side\"\n",
    "    annotation (Placement(transformation(extent={{90,-70},{110,-50}})));\n",
    "protected\n",
    "  Analog.Ideal.IdealTransformer transformer(\n",
    "    final n=n, final considerMagnetization=considerMagnetization,\n",
    "    final Lm1=Lm1)\n",
    "    annotation (Placement(transformation(extent={{-60,-10},{-40,10}})));\n",
    "  Analog.Ideal.IdealDiode D1(final Vknee=0, final Ron=Ron, final Goff=Goff)\n",
    "    annotation (Placement(\n",
    "        transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=45,\n",
    "        origin={-2,10})));\n",
    "  Analog.Basic.Capacitor capacitor(C=C)\n",
    "    annotation (Placement(\n",
    "        transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=270,\n",
    "        origin={70,-20})));\n",
    "  Analog.Ideal.IdealDiode D2(final Vknee=0, final Ron=Ron, final Goff=Goff)\n",
    "    annotation (Placement(\n",
    "        transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=-45,\n",
    "        origin={22,10})));\n",
    "  Analog.Ideal.IdealDiode D3(final Vknee=0, final Ron=Ron, final Goff=Goff)\n",
    "    annotation (Placement(\n",
    "        transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=-45,\n",
    "        origin={-2,-10})));\n",
    "  Analog.Ideal.IdealDiode D4(final Vknee=0, final Ron=Ron, final Goff=Goff)\n",
    "    annotation (Placement(\n",
    "        transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=45,\n",
    "        origin={22,-10})));\n",
    "equation\n",
    "  connect(D3.n,D4. p) annotation (Line(\n",
    "      points={{5.07107,-17.0711},{10,-18},{14.9289,-17.0711}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(D1.n,D2. p) annotation (Line(\n",
    "      points={{5.07107,17.0711},{10,18},{14.9289,18},{14.9289,17.0711}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(D1.p,D3. p) annotation (Line(\n",
    "      points={{-9.07107,2.92893},{-10,0},{-9.07107,-2.92893}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(D2.n,D4. n) annotation (Line(\n",
    "      points={{29.0711,2.92893},{30,0},{29.0711,-2.92893}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(transformer.p2,D1. n) annotation (Line(\n",
    "      points={{-40,5},{-40,24},{10,24},{10,17.0711},{5.07107,17.0711}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(D4.p,transformer. n2) annotation (Line(\n",
    "      points={{14.9289,-17.0711},{10,-17.0711},{10,-30},{-40,-30},{-40,-5}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(D1.p, capacitor.n) annotation (Line(\n",
    "      points={{-9.07107,2.92893},{-14,2.92893},{-14,-42},{70,-42},{70,-30}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(transformer.n1, gnd) annotation (Line(\n",
    "      points={{-60,-5},{-60,-60},{-100,-60}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(transformer.n2, gnd) annotation (Line(\n",
    "      points={{-40,-5},{-40,-60},{-100,-60}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(transformer.p1, p) annotation (Line(\n",
    "      points={{-60,5},{-60,60},{-100,60}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(capacitor.p, D2.n) annotation (Line(\n",
    "      points={{70,-10},{70,2.92893},{29.0711,2.92893}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(capacitor.p, p_load) annotation (Line(\n",
    "      points={{70,-10},{70,60},{100,60}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "  connect(capacitor.n, n_load) annotation (Line(\n",
    "      points={{70,-30},{70,-60},{100,-60}},\n",
    "      color={0,0,255},\n",
    "      smooth=Smooth.None));\n",
    "end BasicPowerSupply;\n",
    "\n",
    "```\n",
    "模型里有几个有趣的地方需要注意。\n",
    "- 首先，我们看到与先前相同的组织结构。参数和接口均为public，而内部组件则为protected。\n",
    "- 我们也看到用Dialog标注来将参数组织为不同的组（这里是\"General\"和\"Transformer\"）。\n",
    "- 我们还看到enable标注上使用了considerMagnetization参数。只有considerMagnetization为真的情况下，Lm1参数才会选择性地有效。\n",
    "\n",
    "使用有层级系统模型，我们不出所料地得到与无层级版本相同的结果：\n",
    "\n",
    "<img src=\"./figures8/SSC.png\" width=600 align=center>\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "可以方便的扩展系统模型，包括额外的负载：\n",
    "\n",
    "<img src=\"./figures8/AdditionalLoad.svg\" width=800 align=center>\n",
    "\n",
    "在这种情况下，如果我们仿真模型就可以看到额外负载将会对电源输出质量的影响：\n",
    "\n",
    "<img src=\"./figures8/SSC_AL.png\" width=800 align=center>\n",
    "\n",
    "通过增大电源内的电容就可以减少的电压波动的幅度，例如：\n",
    "\n",
    "<img src=\"./figures8/SSC_ALC.png\" width=800 align=center>\n",
    "\n",
    "但是，如果我们增加电容容量过大，我们会发现电源输出对负载变化的响应变得非常慢，如：\n",
    "\n",
    "<img src=\"./figures8/SSC_ALC2.png\" width=800 align=center>\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 结论\n",
    "\n",
    "这个例子再次说明了组件的集合如何能组织成可重复使用的子系统模型。这个例子遵从了放置参数的最佳实践。而连接器在所得子系统模型的public区域。同时，内部组件则留在protected区域内。"
   ]
  }
 ],
 "metadata": {
  "anaconda-cloud": {},
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
