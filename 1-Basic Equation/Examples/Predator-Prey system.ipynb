{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 猎食者猎物系统\n",
    "***\n",
    "目前为止，我们已经介绍了热学、电学和机械方面的实例。实际上，这些都是工程实例。然而，Modelica语言并不仅限于工程科学。为了强化这一点，本节将介绍基于猎食者和猎物之间关系的共同生态系统动力学模型。\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 经典猎食者猎物系统\n",
    "\n",
    "经典的猎食者猎物模型涉及两个物种。一个物种叫做“猎物”。本节中，其物种种群用$x$表示。另一个物种称为“猎食者”，其物种种群用$y$表示。\n",
    "\n",
    "\n",
    "有三个因素对猎食者猎物系统影响较大。首先是**“猎物”物种的繁殖率**。假设其繁殖率和种群成正比。可以理解为生存环境中潜在的配偶越多，种群的繁殖率就会越高。我们可以用数学公式表达如下：\n",
    "\n",
    "$$\\dot x_r = \\alpha x$$\n",
    "\n",
    "其中，$x$表示猎物的种群，$\\alpha$是表示种群繁殖率的比例常数，$\\dot x_r$表示**由于繁殖**引起的种群变化。\n",
    "\n",
    "其次影响猎食者猎物系统的因素是**猎食者的饥饿程度**。如果没有足够的“猎物”来充饥，部分猎食者就会饿死。在建模饥饿程度时，要考虑竞争关系对其的重要影响。我们也是用**比例关系描述上述模型**。不同于繁殖率模型，这次是反比例关系，因为越多的猎食者越容易引发饥饿。在数学表达式上，这和繁殖率模型具有相同的表达形式：\n",
    "\n",
    "$$\\dot y_s=-\\gamma y$$\n",
    "\n",
    "其中，$y$表示猎食者种群。$\\gamma$表示种群饥饿的比例常数。$\\dot y_s$表示**由于饥饿**引起的猎食者种群变化。\n",
    "\n",
    "最后一个我们要考虑的因素是**“捕食”**，即猎食者对猎物的消耗。如果没有天敌，猎物会呈指数增长。因此，捕食是保持猎物种群平衡的重要因素。\n",
    "\n",
    "同理，没有猎物，猎食者也会全部死光。因此，捕食平衡了这种效应，并且防止猎食者种群的消失。因此，我们得到一个比例关系。但是，这个比例关系实际上是一个双线性关系。事实上，猎食者发现并捕获猎物的机会与猎物种群、猎食者种群两者都存在着正比关系。由于这种特殊的影响因素，需要两个物种都参与。因此，这里的数学表达式与前述的繁殖率及猎食者饥饿程度的公式都在结构上有所不同，即：\n",
    "\n",
    "$$\\dot x_p=-\\beta xy$$\n",
    "\n",
    "$$\\dot y_p=-\\delta xy$$\n",
    "\n",
    "其中，$\\dot x_p$表示**由于捕食而导致的猎物种群减少**。$\\dot y_p$表示**由于捕食而导致猎食者种群的增加**。$\\beta$表示捕获猎物概率的比例常数。$\\delta$表示猎食者由于足够的营养以提高繁殖率可能性的比例常数。\n",
    "\n",
    "全面考虑各种因素，可以得到每个种群的整体变化。用以下两个方程表示：\n",
    "\n",
    "$$\\dot x=\\dot x_r+\\dot x_p$$\n",
    "\n",
    "$$\\dot y=\\dot y_r+\\dot y_p$$\n",
    "\n",
    "根据上述两个方程，通过数学运算，可以重新整合方程等式的右侧，得到如下方程组：\n",
    "\n",
    "$$\\dot x=x(\\alpha-\\beta y)$$\n",
    "\n",
    "$$\\dot y=y(\\delta x-\\gamma)$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "根据前几章我们学到的知识，将上述方程转化成Modelica语言应该相当简单：\n",
    "\n",
    "```Modelica\n",
    "model ClassicModel \"This is the typical equation-oriented model\"\n",
    "  parameter Real alpha=0.1 \"Reproduction rate of prey\";\n",
    "  parameter Real beta=0.02 \"Mortality rate of predator per prey\";\n",
    "  parameter Real gamma=0.4 \"Mortality rate of predator\";\n",
    "  parameter Real delta=0.02 \"Reproduction rate of predator per prey\";\n",
    "  parameter Real x0=10 \"Start value of prey population\";\n",
    "  parameter Real y0=10 \"Start value of predator population\";\n",
    "  Real x(start=x0) \"Prey population\";\n",
    "  Real y(start=y0) \"Predator population\";\n",
    "  \n",
    "equation\n",
    "  der(x) = x*(alpha-beta*y);\n",
    "  der(y) = y*(delta*x-gamma);\n",
    "end ClassicModel;\n",
    "```\n",
    "\n",
    "在上述代码中，变量$x$和$y$的**start**属性是第一次看到，用来在声明变量的时通过start属性直接指定其初始条件。这里用来对变量x和y设置所代表的种群初始值。根据前面实例的讲解，可以认为是以下模型的简化形式。\n",
    "\n",
    "```Modelica\n",
    "model ClassicModelInitialEquations \"This is the typical equation-oriented model\"\n",
    "  parameter Real alpha=0.1 \"Reproduction rate of prey\";\n",
    "  parameter Real beta=0.02 \"Mortality rate of predator per prey\";\n",
    "  parameter Real gamma=0.4 \"Mortality rate of predator\";\n",
    "  parameter Real delta=0.02 \"Reproduction rate of predator per prey\";\n",
    "  parameter Real x0=10 \"Initial prey population\";\n",
    "  parameter Real y0=10 \"Initial predator population\";\n",
    "  Real x \"Prey population\";\n",
    "  Real y \"Predator population\";\n",
    "  \n",
    "initial equation\n",
    "  x = x0;\n",
    "  y = y0;\n",
    "  \n",
    "equation\n",
    "  der(x) = x*(alpha-beta*y);\n",
    "  der(y) = y*(delta*x-gamma);\n",
    "end ClassicModelInitialEquations;\n",
    "```\n",
    "\n",
    "值得注意的是，这种方法既有优点也有缺点。方法的**优点是比较灵活**。其实，start属性更多的是一种绑定关系的暗示。如果Modelica编译器将某个特定变量识别为**状态**（即某个变量需要一个初始化条件），**并且模型的initial equation部分没有提供非充足的初始条件，这时start属性就可以作为替代，声明变量的初始条件**。换句话说，你可以认为start属性是在必须具备初始条件的情况下的**“后备初始条件”**。\n",
    "\n",
    "你需要注意start属性所具备的一些缺点。首先，此属性只是一个完全可以忽略的提示工具。其次，其是否被忽略也很难预测。因为哪些变量作为状态在不同的工具里选择也会不同。\n",
    "\n",
    "避免上述缺点我们可以使用**fixed属性**（在内建类型章节中作进一步讨论）。**变量的fixed属性被用来通知编译器start属性必须作为初始条件来使用**。换句话说，如下的initial equation部分：\n",
    "\n",
    "```Modelica\n",
    "  Real x;\n",
    "initial equation\n",
    "  x = 5;\n",
    "```\n",
    "\n",
    "等同于下述变量声明中关于fixed和start属性的使用：\n",
    "\n",
    "```Modelica\n",
    "Real x(start=5, fixed=true);\n",
    "```\n",
    "\n",
    "最后，start属性的另一个复杂之处是其功能被“重载”了。这就意味着，此属性有两种不同的用途。如果，所讨论的变量不是一个状态，而是一个“迭代变量”（即变量的解依赖于非线性方程组），那么start属性可以通过编译器作为初始的猜测值（即非线性求解方程首次迭代时使用的变量值）。\n",
    "\n",
    "是否指定一个变量的start属性取决于你希望初始条件会得到多严格的。\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "不管使用哪种初始化的方法，这些模型的仿真结果都是相同的。猎食者猎物系统的典型结果如下图所示：\n",
    "\n",
    "<img src=\"./figure1/11.png\" width=\"500\" align=center>\n",
    "\n",
    "$$经典猎食者猎物模型$$\n",
    "\n",
    "通过上图，可以看到每个种群的周期性行为。最初，没有充足的食物来支撑较多的猎食者。存在的猎食者捕食任何可以找到的猎物。但即便如此，饥饿还是会发生并且导致猎食者种群数量的下降。在此期间，猎食者消耗猎物种群的速率如此之高，以致于猎物种群的繁殖速率不足以弥补因捕食而减少的种群数。因此，猎物种群的数量也在下降。\n",
    "\n",
    "在某些点上，猎食者种群的数量变的如此之低，导致猎物种群的繁殖数量高于因捕食而减少的种群数。猎物物种的数量开始反弹。因为猎食者种群大小的回升需要较长的时间。所以在此期间，猎物种群的增长速度几乎不受猎食者种群的影响。最终，由于猎物的丰盛，猎食者种群的数量开始回升，之至系统恢复到猎食者和猎物种群的原始状态，然后整个循环再重演，无穷无尽。\n",
    "\n",
    "该系统一次次的返回到相同的初始条件处（当然要忽略数值计算错误）。这一现象是系统最有趣的地方之一。特别是考虑猎食者猎物系统方程实际上是非线性的，这也就更加令人注目了。\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 稳定状态初始化\n",
    "\n",
    "让我们想象一下，这些物种种群的极端波动可能会导致一些不良的生态后果。在这种情况下，理解减少或消除这些波动的方法对整改系统将会很有帮助。一个简单的方法就是使两个种群维持在相对平衡的状态。但是，如何使用这些模型来帮助我们确定这样的“稳定”状态？\n",
    "\n",
    "上述问题的答案就在于**初始化条件**。我们可以用能描述系统处于平衡状态的方程来初始化系统，而不是直接给猎食者和猎物种群指定初始值。幸运的是，Modelica包含足够丰富的初始化方法，允许我们指定上述（或者其他）类型的初始化条件。\n",
    "\n",
    "为了确保我们的系统开始的时候处于平衡状态，我们只需要简单的定义什么是平衡。从数学描述上来说，系统如果满足以下两个条件，就可以说该系统是处于平衡状态的：\n",
    "\n",
    "$$\\dot x=0$$\n",
    "\n",
    "$$\\dot y=0$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "为了在Modelica模型中获取上述特征，我们只需在initial equation区域增加以下方程，如下所示：\n",
    "\n",
    "```Modelica\n",
    "model QuiescentModel \"Find steady state solutions to LotkaVolterra equations\"\n",
    "  parameter Real alpha=0.1 \"Reproduction rate of prey\";\n",
    "  parameter Real beta=0.02 \"Mortality rate of predator per prey\";\n",
    "  parameter Real gamma=0.4 \"Mortality rate of predator\";\n",
    "  parameter Real delta=0.02 \"Reproduction rate of predator per prey\";\n",
    "  Real x \"Prey population\";\n",
    "  Real y \"Predator population\";\n",
    "  \n",
    "initial equation\n",
    "  der(x) = 0;\n",
    "  der(y) = 0;\n",
    "  \n",
    "equation\n",
    "  der(x) = x*(alpha-beta*y);\n",
    "  der(y) = y*(delta*x-gamma);\n",
    "end QuiescentModel;\n",
    "```\n",
    "\n",
    "上述模型和之前模型最大的区别就是包含突出显示的初始化方程。回到上述模型，你可能想知道那些初始化方程到底意味着什么？毕竟，我们需要求解的是变量$x$和$y$。但是这些变量甚至都没有出现在我们的初始化方程内。系统是如何对这些变量求解的？\n",
    "\n",
    "问题的答案在于理解函数$x(t)$和$y(t)$均是通过对带初始条件的微分方程进行积分来求解的。在仿真过程中，我们可以看到$x$和$\\dot x$是通过下面的方程进行“耦合”的：\n",
    "\n",
    "$$x(t)=\\int_{t_0}^{t_f}\\dot x dx + x(t_0)$$\n",
    "\n",
    "（当然了，在$y$和$\\dot y$之间也存在着类似的关系）\n",
    "\n",
    "但是，在系统初始化的过程中（即在计算初始条件时），上述关系是不成立的。在这种情况下，$x$和$\\dot x$之间并不存在“耦合”关系（对$y$和$\\dot y$也并不适用）。换句话说，即便知道变量$x$和$y$是如何定义的也不能提供求解方程$\\dot x$或$\\dot y$的线索。在初始化问题上，我们可以认为$x$、$y$、$\\dot x$和$\\dot y$是四个相互独立的变量。\n",
    "\n",
    "换一种说法，即在仿真的过程中，我们通过对$\\dot x$进行积分来求解$x$。因此，积分方程是用于求解$x$的方程。但是在初始化过程中，我们不能用这个等式，所以（对每个在仿真过程中求解的微分方程），我们需要一个额外的方程。\n",
    "\n",
    "任何情况下，我们要明白是在初始化过程中需要四个不同的方程来得到唯一解。在我们的QuiescentModel模型中，这四个方程如下所示：\n",
    "\n",
    "$$\\dot x=0$$\n",
    "\n",
    "$$\\dot x=x(\\alpha-\\beta y)$$\n",
    "\n",
    "$$\\dot y=0$$\n",
    "\n",
    "$$\\dot y=y(\\delta x-\\gamma)$$\n",
    "\n",
    "这些方程之间**并不相互矛盾**，理解这一点非常重要。如果你有编程背景，对前面两个方程可能会有疑问。“到底$\\dot x$取值是多少？是零？还是$x(\\alpha-\\beta y)$？”问题的答案是都是。没有理由认为这两个方程不可能同时为真。\n",
    "\n",
    "重要的是要记住，这些方程不具备赋值语句的功能。下面系统的方程与上述系统在数学表达上是完全一样的，并且清楚的展现了$x$和$y$是如何求解的：\n",
    "\n",
    "$$\\dot x=0$$\n",
    "\n",
    "$$\\dot y=0$$\n",
    "\n",
    "$$x(\\alpha-\\beta y)=\\dot x$$\n",
    "\n",
    "$$y(\\delta x-\\gamma)=\\dot y$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "在这种格式下，我们更容易理解如何对$x$和$y$值进行求解。首先要注意的是，我们不能显式求解变量$x$和$y$的值。换句话说，如果没有变量$x$出现在等式的右边，我们就不能将这些方程的形式变换为$x=...$。所以我们不得不接受，这个方程组同时包含变量$x$和$y$。\n",
    "\n",
    "但是，更加复杂的是该仿真系统是非线性的（这也是为什么我们不能用线性代数将其变换为显式方程组）。事实上，如果我们仔细研究这些方程，可以发现存在两种可能解。一个解是平凡解（$x=0;y=0$），另一个则是非零解。\n",
    "\n",
    "如果我们试着仿真建立的**QuiescentModel模型**，会出现什么样的结果呢？仿真结果如下图所示：\n",
    "\n",
    "<img src=\"./figure1/12.png\" width=\"500\" align=center>\n",
    "\n",
    "根据第一种解，猎食者和猎物的种群数都变为了零。这种情况下系统没有繁殖、捕食以及饥饿。因为上述影响因素都和物种种群数成比例，而物种种群数都为零。所以系统没有变化。但这并不是一个很有趣的解决方案。\n",
    "\n",
    "因为该系统是非线性的，因此系统方程有两个解。我们怎样才能使非线性求解器远离这个零根呢？\n",
    "\n",
    "还记得，前面提到过start属性是被重载了，在讨论经典猎食者猎物系统模型时，我们曾经指出，如果**具有start属性的变量被选为迭代变量**，start属性的其中一个作用是提供初始化猜想值。在我们的QuiescentModel模型中，恰巧变量$x$和$y$就是迭代变量。因为该变量必须通过系统的非线性方程组来求解。这也就意味着，我们要对变量$x$和$y$的start属性值进行指定，以尽量“避开”系统的零解（或者说至少接近我们期望的非零解）。例如：\n",
    "\n",
    "```Modelica\n",
    "model QuiescentModelUsingStart \"Find steady state solutions to LotkaVolterra equations\"\n",
    "  parameter Real alpha=0.1 \"Reproduction rate of prey\";\n",
    "  parameter Real beta=0.02 \"Mortality rate of predator per prey\";\n",
    "  parameter Real gamma=0.4 \"Mortality rate of predator\";\n",
    "  parameter Real delta=0.02 \"Reproduction rate of predator per prey\";\n",
    "  \n",
    "  Real x(start=10) \"Prey population\";\n",
    "  Real y(start=10) \"Predator population\";\n",
    "  \n",
    "initial equation\n",
    "  der(x) = 0;\n",
    "  der(y) = 0;\n",
    "equation\n",
    "  der(x) = x*(alpha-beta*y);\n",
    "  der(y) = y*(delta*x-gamma);\n",
    "end QuiescentModelUsingStart;\n",
    "```\n",
    "\n",
    "上述模型引导我们设置一组初始条件，使得仿真结果更加符合我们最初的目标（即猎食者和猎物种群大小都得到了非零解）。\n",
    "\n",
    "<img src=\"./figure1/13.png\" width=\"500\" align=center>\n",
    "\n",
    "$$ 静态模型（平凡解）$$\n",
    "\n",
    "值得指出的是（在内建类型章节中将会讨论），**start属性的默认值是零**。这也就是为什么当我们仿真QuiescentModel模型时，我们会恰巧准确的得到了系统的零解。因为这是我们初始的猜测，并且恰巧也是系统的精确解，因此系统就无需进行迭代或求解其他根了。"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 避免重复\n",
    "\n",
    "基于猎食者猎物方程，我们已经讨论过几个不同的模型（ClassicModel，QuiescentModel以及QuiescentModelUsingStart模型）。你有没有注意到这些模型间的共同点？如果你仔细观察，你会发现它们几乎所有的内容都相同。实际上模型之间几乎没有任何差别！\n",
    "\n",
    "在软件工程中，有一种说法是 “冗余是一切罪恶的根源” 。这里的情况也不例外（其实很大程度上如此。因为Modelica代码其实也是属于软件）。目前为止，我们所写的代码维护起来将会特别恼人。这是因为我们发现的任何错误都必须在每个模型中修改。此外，我们所作的任何改进也必须应用到每一个模型上。目前，我们处理的模型数量相对较少，但这种“复制粘贴”的模型开发方法会导致大量只有轻微差异的模型。\n",
    "\n",
    "那么，我们能做些什么来避免上述情况的发生呢？在面向对象的编程语言中，基本上存在两种机制来减少冗余代码。他们是组合（在后面的组件章节中进行讨论）和继承。这里我们只对继承进行简单的介绍。\n",
    "\n",
    "如果我们仔细观察QuiescentModelUsingStart模型，会发现和原来的ClassicModel模型之间几乎没有差别。事实上，唯一的不同之处如下所示：\n",
    "\n",
    "```Modelica\n",
    "model ClassicModel \"This is the typical equation-oriented model\"\n",
    "  parameter Real alpha=0.1 \"Reproduction rate of prey\";\n",
    "  parameter Real beta=0.02 \"Mortality rate of predator per prey\";\n",
    "  parameter Real gamma=0.4 \"Mortality rate of predator\";\n",
    "  parameter Real delta=0.02 \"Reproduction rate of predator per prey\";\n",
    "  parameter Real x0=10 \"Start value of prey population\";\n",
    "  parameter Real y0=10 \"Start value of predator population\";\n",
    "  Real x(start=x0) \"Prey population\";\n",
    "  Real y(start=y0) \"Predator population\";\n",
    "equation\n",
    "  der(x) = x*(alpha-beta*y);\n",
    "  der(y) = y*(delta*x-gamma);\n",
    "end ClassicModel;\n",
    "\n",
    "\n",
    "\n",
    "model QuiescentModelUsingStart \"Find steady state solutions to LotkaVolterra equations\"\n",
    "  parameter Real alpha=0.1 \"Reproduction rate of prey\";\n",
    "  parameter Real beta=0.02 \"Mortality rate of predator per prey\";\n",
    "  parameter Real gamma=0.4 \"Mortality rate of predator\";\n",
    "  parameter Real delta=0.02 \"Reproduction rate of predator per prey\";\n",
    "  Real x(start=10) \"Prey population\";\n",
    "  Real y(start=10) \"Predator population\";\n",
    "  \n",
    "initial equation\n",
    "  der(x) = 0;\n",
    "  der(y) = 0;\n",
    "\n",
    "equation\n",
    "  der(x) = x*(alpha-beta*y);\n",
    "  der(y) = y*(delta*x-gamma);\n",
    "end QuiescentModelUsingStart;\n",
    "```\n",
    "\n",
    "换句话说，唯一的区别是**增加了initial equation区域**（原始的ClassicModel模型已经包含了变量$x$和$y$的非零start属性值）。理想情况下，我们可以通过简单的定义一个模型与另外模型间的差异，就可以由此避免任何冗余代码。事实证明，这也正是**extends关键字**的作用。考虑QuiescentModelUsingStart模型的以下版本：\n",
    "\n",
    "```Modelica\n",
    "model QuiescentModelWithInheritance \"Steady state model with inheritance\"\n",
    "  extends ClassicModel;\n",
    "initial equation\n",
    "  der(x) = 0;\n",
    "  der(y) = 0;\n",
    "end QuiescentModelWithInheritance;\n",
    "```\n",
    "\n",
    "**注意extends关键字的使用**。从概念上讲，“扩展子句”只是简单的要求编译器将另外的模型（本例是ClassicModel模型）插入到所定义的模型中。通过这种方式，我们将从ClassicModel模型中复制（或“继承”）其包含的所有内容，而无需重复定义。因此，除了新加入的初始化方程外，QuiescentModelWithInheritance模型和ClassicModel模型其他部分完全一样。\n",
    "\n",
    "但是，有一种情况，如果我们不想完全精确地继承模型中的内容，该怎么办？例如，如果我们想要改变参数gamma和delta的值？\n",
    "\n",
    "当使用extends功能时，Modelica语言允许我们对模型加入相应的“修改语句”。模型对应的修改语句紧跟在所继承模型的名字后面，如下所示：\n",
    "\n",
    "```Modelica\n",
    "model QuiescentModelWithModifications \"Steady state model with modifications\"\n",
    "  extends QuiescentModelWithInheritance(gamma=0.3, delta=0.01);\n",
    "end QuiescentModelWithModifications;\n",
    "```\n",
    "\n",
    "还要注意的是，我们可以从ClassicModel中继承。但是，随后我们还必须重新定义初始化方程组以获得静止的初始条件。但是，我们不是通过继承QuiescentModelWithModifications模型，而是通过重复使用两个不同模型的内容来避免重复。\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
