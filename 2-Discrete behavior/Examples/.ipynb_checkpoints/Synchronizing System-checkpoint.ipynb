{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 同步系统\n",
    "***\n",
    "\n",
    "Modelica语言3.3版引入了一个新功能以解决有关非确定性离散行为的问题。本节将先介绍这些问题在3.3版本之前的表现。然后，我们将用例子展示这些新特性能如何帮助解决这类问题。\n",
    "\n",
    "```Modelica\n",
    "model IndependentSampling \"Sampling independently\"\n",
    "  Real x \"Sampled at 10Hz via one method\";\n",
    "  Real y \"Sampled at 10Hz via another method\";\n",
    "  Real e \"Error between x and y\";\n",
    "  Real next_time \"Next sample for y\";\n",
    "equation\n",
    "  when sample(0,0.1) then\n",
    "    x = time;\n",
    "  end when;\n",
    "\n",
    "  when {initial(), time>pre(next_time)} then\n",
    "    y = time;\n",
    "    next_time = pre(next_time)+0.1;\n",
    "  end when;\n",
    "  e = x-y;\n",
    "end IndependentSampling;\n",
    "```\n",
    "\n",
    "倘若你仔细观察，你会发现x和y都在离散的时间点上被计算。此外，两变量的采样时点都是在仿真开始时以及之后的每0.1秒。但问题是，它们真的一样吗？为了更容易解决这个问题，我们加入变量e以计算两者间的差。\n",
    "\n",
    "<img src=\"./figure2\\26.png\" width=\"500\" height=\"400\" alt=\"RLC开关电路的电压响应\" align=center />\n",
    "\n",
    "对模型进行仿真，我们会得到如下的x、y轨迹。当然，两个轨迹看上去是一样的。但要真正确定它们之间是否存在任何不同，我们绘制了误差值e：\n",
    "\n",
    "<img src=\"./figure2\\27.png\" width=\"500\" height=\"400\" alt=\"RLC开关电路的电压响应\" align=center />\n",
    "\n",
    "现在，让我们考虑一下模型：\n",
    "\n",
    "```Modelica\n",
    "model SynchronizedSampling \"A simple way to synchronize sampling\"\n",
    "  Integer tick \"A clock counter\";\n",
    "  Real x, y;\n",
    "  Real e \"Error between x and y\";\n",
    "equation\n",
    "  when sample(0,0.1) then\n",
    "    tick = pre(tick)+1;\n",
    "  end when;\n",
    "\n",
    "  when change(tick) then\n",
    "    x = time;\n",
    "  end when;\n",
    "\n",
    "  when change(tick) then\n",
    "    y = time;\n",
    "  end when;\n",
    "\n",
    "  e = x-y;\n",
    "end SynchronizedSampling;\n",
    "```\n",
    "\n",
    "在这里，我们设置了一个共同的信号以触发两个变量的赋值操作。通过这种方式，我们可以确定，当tick信号变为真时，无论是x还是y都将被赋予一个值。显然，运行这个模型后我们可以看到，误差永远为零：\n",
    "\n",
    "<img src=\"./figure2\\28.png\" width=\"500\" height=\"400\" alt=\"RLC开关电路的电压响应\" align=center />\n",
    "\n",
    "在这样的做法里，每个信号都是基于一个共同的“节拍”（或时钟）进行取样的。这是一个很好的避免确定性问题的方式。但是，如果你有两个不同频率的信号，且已知在特定的时点两者会被同时取样，那么情况又是如何呢？请考虑如下例子：\n",
    "\n",
    "```Modelica\n",
    "model SubsamplingWithIntegers \"Use integers to implement subsampling\"\n",
    "  Integer tick \"Clock counter\";\n",
    "  Real x, y, z;\n",
    "equation\n",
    "  when sample(0,0.1) then\n",
    "    tick = pre(tick)+1;\n",
    "  end when;\n",
    "\n",
    "  when change(tick) then\n",
    "    x = time;\n",
    "  end when;\n",
    "\n",
    "  when change(tick) then\n",
    "    y = time;\n",
    "  end when;\n",
    "\n",
    "  when mod(tick-1,2)==0 then\n",
    "    z = time;\n",
    "  end when;\n",
    "end SubsamplingWithIntegers;\n",
    "```\n",
    "\n",
    "\n",
    "在这种情况下，变量tick是计数器。每次此变量改变时，我们会更新x和y的值。所以，直到这一点都是与先前的模型相同的。然而，我们增加了第三个信号z。而该信号仅在当tick值是为奇时才会进行采样。所以相比之下，x和y的采样次数为其的两倍。但我们可以肯定的是，每次z更新时，x和y是在完全相同的时点进行更新。对模型进行仿真我们会得到一下结果：\n",
    "\n",
    "<img src=\"./figure2\\29.png\" width=\"500\" height=\"400\" alt=\"RLC开关电路的电压响应\" align=center />\n",
    "\n",
    "这是Modelica语言在3.3版本之前所采用的方法。但3.3版本引入了一些新的功能以帮助我们表述这些情况。\n",
    "\n",
    "\n",
    "请考虑以下模型：\n",
    "\n",
    "```Modelica\n",
    "model SamplingWithClocks \"Using clocks to sub and super sample\"\n",
    "  Real x, y, z, w;\n",
    "equation\n",
    "  x = sample(time, Clock(1,10));\n",
    "  y = sample(time, Clock(1,10));\n",
    "  z = subSample(x, 2);\n",
    "  w = superSample(x, 3);\n",
    "end SamplingWithClocks;\n",
    "```\n",
    "\n",
    "\n",
    "现在我们不再使用when语句。相反，我们会使用一个增强版的sample（取样）函数。这个sample函数的首个参数是待取样的表达式，第二个参数则是取样的时间间隔。让我们逐行阅读并讨论下面的代码。首先，我们有：\n",
    "\n",
    "  x = sample(time, Clock(1,10));\n",
    "  \n",
    "  \n",
    "注意，我们已经不再使用0.1，而且不再能看到表示为实数值的时钟间隔。相反，我们使用Clock表达式为x定义一个有理数值的时钟间隔。这一点很重要，因为这种做法允许我们对不同时钟进行精确比较。让我们看下一行：\n",
    "\n",
    "\n",
    "  y = sample(time, Clock(1,10));\n",
    "  \n",
    "  \n",
    "再一次，我们看到在时钟间隔的有理表示。这意味着在实践中，Modelica语言编译器可以确信这两个时钟x与y是相同的。因为两者均是用可确切比较的整数量进行定义的。也就是说，在执行仿真时，我们可以确信这两个时钟会同时触发。\n",
    "\n",
    "如果我们想创建一个正好是x两倍慢的时钟，那么我们可以使用subSample运算符来完成。在z的定义里我们可以看到这种情况：\n",
    "\n",
    "\n",
    "  z = subSample(x, 2);\n",
    "  \n",
    "  \n",
    "Modelica语言编译器可以在幕后对这些时钟间的关系进行推理。编译器知道每隔110110秒时钟x就会触发。因此Modelica语言编译器可以使用subSample运算符所提供的信息推论出，每隔210210秒时钟z就会触发。理论上，这意味着z也可以被定义为：\n",
    "\n",
    "\n",
    "z = sample(time, Clock(2,10));\n",
    "\n",
    "\n",
    "但通过使用subSample运算符相对x去进行对z的定义，我们可以确保无论x是如何定义的，z的触发频率总是x的一半。\n",
    "\n",
    "类似地，我们可以使用superSample运算符定义另一个触发频率为x3倍的时钟w。\n",
    "\n",
    "\n",
    "  w = superSample(x, 3);\n",
    "  \n",
    "  \n",
    "同样，我们可以直接使用sample以对w进行如下定义：\n",
    "\n",
    "\n",
    "w = sample(time, Clock(1,30));\n",
    "\n",
    "\n",
    "但使用superSample我们可以确保w的采样速度总是x的三倍、z的六倍（因为z也是基于x定义的）。\n",
    "\n",
    "\n",
    "Modelica语言的同步时钟特性是相对较新。因此，并非所有的Modelica语言编译器都支持这些特性。要了解更多有关这些同步功能及其应用，可以参考[Elmqvist]以及／或者3.3版以后的Modelica规范。\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "anaconda-cloud": {},
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
