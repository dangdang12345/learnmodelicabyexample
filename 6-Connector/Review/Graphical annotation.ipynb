{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 图形标注\n",
    "***\n",
    "\n",
    "\n",
    "虽然本节出现在连接器章内，本话题适用于与一般的模型定义相关的图形标注。因此，这里提供的信息可以对Modelica的许多方面提供有益的参考。\n",
    "\n",
    "\n",
    "## 图形层\n",
    "\n",
    "有两种不同的表现形式可以去描述Modelica的实体的外观。一个是所谓“图标”表示。另一个则是所谓“简图”表示。在Modelica里，图标表示展现的是从“外面”观察模型时看到的内容。一般来说，图标包括一些独特的视觉表现形式。此外，通过替换（稍后我们会进行介绍），它也包含了关于该实体的进一步信息。\n",
    "\n",
    "\n",
    "在另一方面，“简图”表示是用来展现组件的“内部”视图。简图表示通常用于Modelica组件图形化的详细文档。特别是当这些内容对于“图标”视图来说过于仔细的时候。\n",
    "\n",
    "\n",
    "一个定义在“图标”层所展现的外观是由Icon标注指定的（在之前对图形连接器讨论里有简要介绍）。毫无疑问，定义在“简图”层所展现的外观则是由Diagram标注指定的。两者均是直接出现在定义里，不和声明或者extends条款等现有元素相关联。\n",
    "\n",
    "\n",
    "一般来说，大部分的定义都包括一个“图标”的表示，但只有少数有空去包括“简图”表示。不过，事实上尽管是在不同的情况下进行的渲染，图形表示的规范却是相同的。\n",
    "\n",
    "\n",
    ">## 使用Icon的例子\n",
    "\n",
    ">>在这本书的其余部分，我们将利用Icon标注举例介绍图形标注。这些例子对Diagram标注同样有效。但由于Icon标注相对常见，关于图形标注的进一步例子仅会出现Icon标注里。\n",
    "\n",
    "\n",
    "## 通用图形定义\n",
    "\n",
    "下面的定义将在本节中引用：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "type DrawingUnit = Real(final unit=\"mm\");\n",
    "type Point = DrawingUnit[2] \"{x, y}\";\n",
    "type Extent = Point[2]\n",
    "  \"Defines a rectangular area {{x1, y1}, {x2, y2}}\";\n",
    "type Color = Integer[3](min=0, max=255) \"RGB representation\";\n",
    "constant Color Black = zeros(3);\n",
    "type LinePattern = enumeration(None, Solid, Dash, Dot, DashDot, DashDotDot);\n",
    "type FillPattern = enumeration(None, Solid, Horizontal, Vertical,\n",
    "                               Cross, Forward, Backward,\n",
    "                               CrossDiag, HorizontalCylinder,\n",
    "                               VerticalCylinder, Sphere);\n",
    "type BorderPattern = enumeration(None, Raised, Sunken, Engraved);\n",
    "type Smooth = enumeration(None, Bezier);\n",
    "type Arrow = enumeration(None, Open, Filled, Half);\n",
    "type TextStyle = enumeration(Bold, Italic, UnderLine);\n",
    "type TextAlignment = enumeration(Left, Center, Right);\n",
    "\n",
    "record FilledShape \"Style attributes for filled shapes\"\n",
    "  Color lineColor = Black \"Color of border line\";\n",
    "  Color fillColor = Black \"Interior fill color\";\n",
    "  LinePattern pattern = LinePattern.Solid \"Border line pattern\";\n",
    "  FillPattern fillPattern = FillPattern.None \"Interior fill pattern\";\n",
    "  DrawingUnit lineThickness = 0.25 \"Line thickness\";\n",
    "end FilledShape;\n",
    "```\n",
    "\n",
    "\n",
    "另外，很多我们马上会讨论到的标注包括一组在以下record定义里的共同元素：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "partial record GraphicItem\n",
    "  Boolean visible = true;\n",
    "  Point origin = {0, 0};\n",
    "  Real rotation(quantity=\"angle\", unit=\"deg\")=0;\n",
    "end GraphicItem;\n",
    "```\n",
    "\n",
    "\n",
    "为了明晰地介绍表示图形元素的常用标注，我们会以GraphicItem为基础扩展出其他模型。\n",
    "\n",
    "\n",
    "## Icon和Diagram标注\n",
    "\n",
    "\n",
    "以下数据描述了应该出现在模型图标层的元素：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "record Icon \"Representation of the icon layer\"\n",
    "  CoordinateSystem coordinateSystem(extent = {{-100, -100}, {100, 100}});\n",
    "  GraphicItem[:] graphics;\n",
    "end Icon;\n",
    "```\n",
    "\n",
    "\n",
    "其中坐标系统中的数据被定义为：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "record CoordinateSystem\n",
    "  Extent extent;\n",
    "  Boolean preserveAspectRatio=true;\n",
    "  Real initialScale = 0.1;\n",
    "  DrawingUnit grid[2];\n",
    "end CoordinateSystem;\n",
    "```\n",
    "\n",
    "\n",
    "换言之，该Icon标注包括了coordinateSystem定义内的坐标系信息。而且，标注还包括了graphics内的图形物件数组。\n",
    "\n",
    "\n",
    "Diagram标注的定义也是一样：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "record Diagram \"Representation of the diagram layer\"\n",
    "  CoordinateSystem coordinateSystem(extent = {{-100, -100}, {100, 100}});\n",
    "  GraphicItem[:] graphics;\n",
    "end Diagram;\n",
    "```\n",
    "\n",
    "\n",
    "## 图形物件\n",
    "\n",
    "Modelica标准里有许多不同的图形物件可以放入Icon或Diagram标注内的graphics矢量。其定义在这里列出以供参考。\n",
    "\n",
    "\n",
    "### Line\n",
    "\n",
    "\n",
    "```Modelica\n",
    "record Line\n",
    "  extends GraphicItem;\n",
    "  Point points[:];\n",
    "  Color color = Black;\n",
    "  LinePattern pattern = LinePattern.Solid;\n",
    "  DrawingUnit thickness = 0.25;\n",
    "  Arrow arrow[2] = {Arrow.None, Arrow.None} \"{start arrow, end arrow}\";\n",
    "  DrawingUnit arrowSize=3;\n",
    "  Smooth smooth = Smooth.None \"Spline\";\n",
    "end Line;\n",
    "```\n",
    "\n",
    "\n",
    "### Polygon\n",
    "\n",
    "\n",
    "```Modelica\n",
    "record Polygon\n",
    "  extends GraphicItem;\n",
    "  extends FilledShape;\n",
    "  Point points[:];\n",
    "  Smooth smooth = Smooth.None \"Spline outline\";\n",
    "end Polygon;\n",
    "```\n",
    "\n",
    "\n",
    "### Rectangle\n",
    "\n",
    "\n",
    "```Modelica\n",
    "record Rectangle\n",
    "  extends GraphicItem;\n",
    "  extends FilledShape;\n",
    "  BorderPattern borderPattern = BorderPattern.None;\n",
    "  Extent extent;\n",
    "  DrawingUnit radius = 0 \"Corner radius\";\n",
    "end Rectangle;\n",
    "```\n",
    "\n",
    "\n",
    "### Ellipse\n",
    "\n",
    "\n",
    "```Modelica\n",
    "record Ellipse\n",
    "  extends GraphicItem;\n",
    "  extends FilledShape;\n",
    "  Extent extent;\n",
    "  Real startAngle(quantity=\"angle\", unit=\"deg\")=0;\n",
    "  Real endAngle(quantity=\"angle\", unit=\"deg\")=360;\n",
    "end Ellipse;\n",
    "```\n",
    "\n",
    "\n",
    "### Text\n",
    "\n",
    "\n",
    "```Modelica\n",
    "record Text\n",
    "  extends GraphicItem;\n",
    "  extends FilledShape;\n",
    "  Extent extent;\n",
    "  String textString;\n",
    "  Real fontSize = 0 \"unit pt\";\n",
    "  String fontName;\n",
    "  TextStyle textStyle[:];\n",
    "  Color textColor=lineColor;\n",
    "  TextAlignment horizontalAlignment = TextAlignment.Center;\n",
    "end Text;\n",
    "```\n",
    "\n",
    "\n",
    "\n",
    "### Bitmap\n",
    "\n",
    "\n",
    "```Modelica\n",
    "record Bitmap\n",
    "  extends GraphicItem;\n",
    "  Extent extent;\n",
    "  String fileName \"Name of bitmap file\";\n",
    "  String imageSource \"Base64 representation of bitmap\";\n",
    "end Bitmap;\n",
    "```\n",
    "\n",
    "\n",
    "## 继承图形标注\n",
    "\n",
    "当一个模型定义从另一个定义继承时，图形标注在默认情况下也会继承。不过，这种行为可以通过对extends子句添加以下标注来控制（分别对应图标层和简图层）：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "record IconMap\n",
    "  Extent extent = {{0, 0}, {0, 0}};\n",
    "  Boolean primitivesVisible = true;\n",
    "end IconMap;\n",
    "\n",
    "record DiagramMap\n",
    "  Extent extent = {{0, 0}, {0, 0}};\n",
    "  Boolean primitivesVisible = true;\n",
    "end DiagramMap;\n",
    "```\n",
    "\n",
    "\n",
    "在这两种情况下， extent数据允许继承图形元素的位置进行调整。将primitivesVisible设置为false则会隐藏所继承的图形元素。\n",
    "\n",
    "\n",
    "## 替换\n",
    "\n",
    "Text标注内的textString字段可以包含替换模式。标注支持以下的替换模式：\n",
    "\n",
    "- %name － 这个字符串模式会被给定定义的实例名称所取代。\n",
    "- %class － 这个字符串模式会被这个定义的名称所取代。\n",
    "\n",
    "- %$<name>$，其中$<name>$为参数名称 - 这个模式会被替换为相应参数的值。\n",
    "\n",
    "- %% － 这个模式会被替换为%。\n",
    "\n",
    "\n",
    "## 组合以上内容\n",
    "\n",
    "在讨论了图形标注的这些方面后，让我们回顾在对图形连接器的讨论中提出的图标定义。\n",
    "\n",
    "\n",
    "```Modelica\n",
    "      Icon(graphics={\n",
    "          Ellipse(\n",
    "            extent={{-100,100},{100,-100}},\n",
    "            lineColor={0,0,255},\n",
    "            fillColor={85,170,255},\n",
    "            fillPattern=FillPattern.Solid),\n",
    "          Rectangle(\n",
    "            extent={{-10,58},{10,-62}},\n",
    "            fillColor={0,128,255},\n",
    "            fillPattern=FillPattern.Solid,\n",
    "            pattern=LinePattern.None),\n",
    "          Rectangle(\n",
    "            extent={{-60,10},{60,-10}},\n",
    "            fillColor={0,128,255},\n",
    "            fillPattern=FillPattern.Solid,\n",
    "            pattern=LinePattern.None,\n",
    "            lineColor={0,0,0}),\n",
    "          Text(\n",
    "            extent={{-100,-100},{100,-140}},\n",
    "            lineColor={0,0,255},\n",
    "            fillColor={85,170,255},\n",
    "            fillPattern=FillPattern.Solid,\n",
    "            textString=\"%name\")}),\n",
    "```\n",
    "\n",
    "\n",
    "这里我们看到了与PositivePin定义相关联的annotation是个模型标注。此外，我们可以看到这个标注内Icon元素包含一系列的图形物体。第一个图形物体是Ellipse标注。紧接着的是两个Rectangle标注。最后是Text（这里使用了前面所讨论的替换）。\n",
    "\n",
    "\n",
    "注意数据在annotation里的呈现方式与我们在前面讨论过的记录（record）定义中描述的数据一致。"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "anaconda-cloud": {},
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
