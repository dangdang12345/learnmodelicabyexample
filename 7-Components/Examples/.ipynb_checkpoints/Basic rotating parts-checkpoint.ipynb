{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 基本旋转组件\n",
    "***\n",
    "\n",
    "\n",
    "在这一节中，我们将介绍如何为一维转动系统创建基本组件。我们将以此前对旋转连接器的讨论为基础，展示如何将连接器应用于定义基本旋转组件的接口。最后，我们将展示如何将这些旋转部件可以被组装成系统模型。这里的系统模型将与第一章内的以方程为基础的旋转系统模型有着相同的行为。\n",
    "\n",
    "\n",
    "## 组件模型\n",
    "\n",
    "在第一章中，我们严格地利用方程式（即无组件模型）去建模了机械示例。在本节中，我们将通过使用组件重新开始系统模型。要做到这一点，我们首先要定义需要的基本组件模型。这些将包括惯性、弹簧、阻尼器和机械地面的模型。\n",
    "\n",
    "\n",
    "正如上一节，我们将首先通过累赘的方法定义组件模型。然后，我们将重新审视这些定义，并尝试将公共代码分解出来，以避免在组件模型里的重复。\n",
    "\n",
    "\n",
    "### 坐标系\n",
    "\n",
    "创建这些模型的方法与我们在传热和电气域上创建组件模型时的方法非常类似。但是，在开始建立组件模型之前，我们应该首先讨论与机械系统相关的一个复杂性：坐标系。\n",
    "\n",
    "在机械领域，我们研究是守恒量是动量。与此前介绍了的守恒量热量和电荷不同，动量有方向性。我们在这里仅考虑一维的情况。因此，这个方向性的后果是，动量是一个带符号的量（即动量可正可负）。\n",
    "\n",
    "\n",
    "考虑一个转动惯量为$J$的转动质量元件。如果在惯量元件的角位置由$\\varphi$表示，则惯量的角速度$\\omega$就定义为：\n",
    "\n",
    "$$\\omega=\\dot {\\varphi}$$\n",
    "\n",
    "显然，正的$\\omega$值将导致$\\varphi$随着时间的推移增加。此外，惯量元件的角加速度$\\alpha$定义为：\n",
    "\n",
    "$$\\alpha=\\dot \\omega$$\n",
    "\n",
    "\n",
    "和角速度一样，我们可以看到，正的$\\alpha$将导致角速度增加。最后，惯量元件的角动量被定义为$J\\omega$。我们从欧拉运动定律可以知道（假设$J$为常数）：\n",
    "\n",
    "$$J\\frac{d\\omega}{dt}=\\tau$$\n",
    "\n",
    "显然由此关系可得，正的转矩$\\tau$将增加存储在转动质量元件里的动量。\n",
    "\n",
    "\n",
    "之所以展示所有这些关系，是为了强调与$\\varphi、\\omega、\\alpha$以及$\\tau$相关的符号规则。这些规则都与角位移正负的基本定义有关。让$\\varphi$增加的方向就是正速度、正加速度、正转矩的方向。\n",
    "\n",
    "\n",
    "### 转动惯量元件\n",
    "\n",
    "我们已经讨论正负号规则以及坐标系。现在，我们就可以开始创建组件模型。首先，我们创建惯量模型：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.VerboseApproach;\n",
    "model Inertia \"Rotational inertia without inheritance\"\n",
    "  parameter Modelica.SIunits.Inertia J;\n",
    "  Modelica.Mechanics.Rotational.Interfaces.Flange_a flange_a\n",
    "    annotation (Placement(transformation(extent={{-110,-10},{-90,10}})));\n",
    "  Modelica.Mechanics.Rotational.Interfaces.Flange_b flange_b\n",
    "    annotation (Placement(transformation(extent={{90,-10},{110,10}})));\n",
    "protected\n",
    "  Modelica.SIunits.Angle phi_rel;\n",
    "  Modelica.SIunits.Torque tau;\n",
    "  Modelica.SIunits.AngularVelocity w;\n",
    "equation\n",
    "  // Variables\n",
    "  phi_rel = flange_a.phi-flange_b.phi;\n",
    "  tau = flange_a.tau;\n",
    "  w = der(flange_a.phi);\n",
    "\n",
    "  // Conserviation of angular momentum (includes storage)\n",
    "  J*der(w) = flange_a.tau + flange_b.tau;\n",
    "\n",
    "  // Kinematic constraint (inertia is rigid)\n",
    "  phi_rel = 0;\n",
    "  annotation ( Icon(graphics={\n",
    "          extent={{-100,90},{100,50}},\n",
    "```\n",
    "\n",
    "\n",
    "Inertia模型包括两个“法兰”，两端各一个。这些法兰的意义是可以更清楚地从Inertia模型的图标看出：\n",
    "\n",
    "<img src=\"./figure7\\10.png\">\n",
    "\n",
    "\n",
    "换言之，Inertia模型在每一端都有一个法兰。你可以把这个模型看作两端都有接口的轴。\n",
    "\n",
    "\n",
    "现在，Inertia模型所需要表示的基本公式为：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "  J*der(w) = flange_a.tau + flange_b.tau;\n",
    "```\n",
    "\n",
    "这表达的基本事实为，惯量元素内储存的动量增加率等于施加其上的转矩之和。回想一下，在之前对非因果连接的讨论里，我们所规定的连接器流变量（这里为flange_a.tau和flange_b.tau）的符号规则：正值表示保守量的流在进入到组件模型。flange_a和flange_b有相同的符号约定这一点，意味着Inertia模型是对称的（即可以在翻转后保持行为不变）。\n",
    "\n",
    "\n",
    "不过，这个等式引用了内部变量w（代表$\\omega$）和$\\tau$。所以我们也需要包含这些变量的声明和定义。\n",
    "\n",
    "\n",
    "### 弹簧模型\n",
    "\n",
    "接下来，让我们考虑一个弹簧模型的定义：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.VerboseApproach;\n",
    "model Spring \"Rotational spring without inheritance\"\n",
    "  parameter Modelica.SIunits.RotationalSpringConstant c;\n",
    "  Modelica.Mechanics.Rotational.Interfaces.Flange_a flange_a\n",
    "    annotation (Placement(transformation(extent={{-110,-10},{-90,10}})));\n",
    "  Modelica.Mechanics.Rotational.Interfaces.Flange_b flange_b\n",
    "    annotation (Placement(transformation(extent={{90,-10},{110,10}})));\n",
    "protected\n",
    "  Modelica.SIunits.Angle phi_rel;\n",
    "  Modelica.SIunits.Torque tau;\n",
    "equation\n",
    "  // Variables\n",
    "  phi_rel = flange_a.phi-flange_b.phi;\n",
    "  tau = flange_a.tau;\n",
    "\n",
    "  // No storage of angular momentum\n",
    "  flange_a.tau + flange_b.tau = 0;\n",
    "\n",
    "  // Hooke's law\n",
    "  tau = c*phi_rel;\n",
    "end Spring;\n",
    "```\n",
    "\n",
    "\n",
    "弹簧模型的图标如下所示：\n",
    "\n",
    "\n",
    "<img src=\"./figure7\\11.png\">\n",
    "\n",
    "\n",
    "和Inertia模型一样，Spring模型有两个连接器，一端各一个。模型还定义了许多相同的内部变量。最终，弹簧的行为可以总结为如下公式：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "  // Hooke's law\n",
    "  tau = c*phi_rel;\n",
    "end Spring;\n",
    "```\n",
    "\n",
    "事实上，除了上述方程和参数c以外，Spring模型的内容与Inertia模型内容大部分是一样的。\n",
    "\n",
    "\n",
    "\n",
    "### 阻尼器模型\n",
    "\n",
    "Damper模型也和Spring模型非常相似。同样，主要区别是参数（这里是d），以及一个方程：\n",
    "\n",
    "\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.VerboseApproach;\n",
    "model Damper \"Rotational damper without inheritance\"\n",
    "  parameter Modelica.SIunits.RotationalDampingConstant d;\n",
    "  Modelica.Mechanics.Rotational.Interfaces.Flange_a flange_a\n",
    "    annotation (Placement(transformation(extent={{-110,-10},{-90,10}})));\n",
    "  Modelica.Mechanics.Rotational.Interfaces.Flange_b flange_b\n",
    "    annotation (Placement(transformation(extent={{90,-10},{110,10}})));\n",
    "protected\n",
    "  Modelica.SIunits.Angle phi_rel;\n",
    "  Modelica.SIunits.Torque tau;\n",
    "equation\n",
    "  // Variables\n",
    "  phi_rel = flange_a.phi-flange_b.phi;\n",
    "  tau = flange_a.tau;\n",
    "\n",
    "  // No storage of angular momentum\n",
    "  flange_a.tau + flange_b.tau = 0;\n",
    "\n",
    "  // Damping relationship\n",
    "  tau = d*der(phi_rel);\n",
    "end Damper;\n",
    "```\n",
    "\n",
    "\n",
    "Damper模型的图标如下所示：\n",
    "\n",
    "\n",
    "\n",
    "<img src=\"./figure7\\12.png\">\n",
    "\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## DRY组件模型\n",
    "\n",
    "我们已经有了惯性元件、弹簧和阻尼器的模型。要完成此前的双弹簧质量阻尼系统唯一缺少的模型就是机械地面的模型了。但在此之前，让我们花一点时间来重新审视这些模型。我们的目标是抽取出这些模型之间的大量共同代码。和上节一样，让我们慢慢来去实践DRY（不要重复）原则。\n",
    "\n",
    "\n",
    "### 共同代码\n",
    "\n",
    "\n",
    "值得一提的是，由于Modelica的标准库拥有大量的旋转部件，几乎从一开始，标准库就要处理冗余代码的问题。不过，我们不会在这里使用Modelica标准库的partial模型。原因简单，这些模型都在设计时都考虑了许多与本节讨论不相关的其他情况。因此，这些（虽然必要的）复杂性使得上述模型不适用于教学。\n",
    "\n",
    "\n",
    "但我们将从Modelica的标准库学习一点。这就是多个partial模型的必要性。这是因为，不像此前讨论的电气部件，我们的转动组件模型彼此间共享不同的代码。\n",
    "\n",
    "\n",
    "一个相同点在于，所有模型均带有两个法兰连接器flange_a和flange_b 。然而，虽然Inertia模型能够存储角动量，但Spring和Damper模型则不能。其结果是，这些组件之间有不同的守恒方程。\n",
    "\n",
    "\n",
    "让我们从所有三种模型共有的元素开始。共有元素由以下的TwoFlange模型表示：\n",
    "\n",
    "\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.Interfaces;\n",
    "partial model TwoFlange\n",
    "  \"Definition of a partial rotational component with two flanges\"\n",
    "\n",
    "  Modelica.Mechanics.Rotational.Interfaces.Flange_a flange_a\n",
    "    annotation (Placement(transformation(extent={{-110,-10},{-90,10}})));\n",
    "  Modelica.Mechanics.Rotational.Interfaces.Flange_b flange_b\n",
    "    annotation (Placement(transformation(extent={{90,-10},{110,10}})));\n",
    "protected\n",
    "  Modelica.SIunits.Angle phi_rel;\n",
    "equation\n",
    "  phi_rel = flange_a.phi-flange_b.phi;\n",
    "end TwoFlange;\n",
    "```\n",
    "\n",
    "\n",
    "\n",
    "除了定义的两个法兰flange_a和flange_b外，这个模型还定义了两法兰间的相对角度，即phi_rel。当然，这种模型也被标记为partial。因为模型缺少对组件行为的任何描述。\n",
    "\n",
    "\n",
    "所有的三种模型都可以继承上述模型。但是，Spring和Damper模型之间仍会有一些冗余的方程式。因此，我们将转而创建TwoFlange模型的一个稍为特别的版本。这个版本表示不存储动量的可压缩模型：\n",
    "\n",
    "\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.Interfaces;\n",
    "partial model Compliant \"A compliant rotational component\"\n",
    "  extends ModelicaByExample.Components.Rotational.Interfaces.TwoFlange;\n",
    "protected\n",
    "  Modelica.SIunits.Torque tau;\n",
    "equation\n",
    "  tau = flange_a.tau;\n",
    "  flange_a.tau + flange_b.tau = 0\n",
    "    \"Conservation of angular momentum (no storage)\";\n",
    "end Compliant;\n",
    "```\n",
    "\n",
    "\n",
    "\n",
    "Compliant模型加上额外的内部变量（以代表从flange_a进入该组件然后传递到flange_b的转矩）。另外，模型也加入了方程，以表示组件不会存储任何角动量。\n",
    "有了这些定义的基类，让我们快速重构各个组件模型的定义。这就可以看出继承能减少多少冗余。\n",
    "\n",
    "\n",
    "\n",
    "### 转动惯量元件\n",
    "\n",
    "凭借TwoFlanges模型，我们的Inertia模型可以简化为：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.Components;\n",
    "model Inertia \"A rotational inertia model\"\n",
    "  parameter Modelica.SIunits.Inertia J;\n",
    "  extends ModelicaByExample.Components.Rotational.Interfaces.TwoFlange;\n",
    "  Modelica.SIunits.AngularVelocity w \"Angular Velocity\"\n",
    "    annotation(Dialog(group=\"Initialization\", showStartAttribute=true));\n",
    "  Modelica.SIunits.Angle phi \"Angle\"\n",
    "    annotation(Dialog(group=\"Initialization\", showStartAttribute=true));\n",
    "equation\n",
    "  phi = flange_a.phi;\n",
    "  w = der(flange_a.phi) \"velocity of inertia\";\n",
    "  phi_rel = 0 \"inertia is rigid\";\n",
    "  J*der(w) = flange_a.tau + flange_b.tau\n",
    "    \"Conservation of angular momentum with storage\";\n",
    "end Inertia;\n",
    "```\n",
    "\n",
    "\n",
    "### 弹簧模型\n",
    "\n",
    "用相同的方式继承了Compliant模型后，Spring模型可以更紧凑地表示为：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.Components;\n",
    "model Spring \"A rotational spring component\"\n",
    "  parameter Modelica.SIunits.RotationalSpringConstant c;\n",
    "  extends ModelicaByExample.Components.Rotational.Interfaces.Compliant;\n",
    "equation\n",
    "  tau = c*phi_rel \"Hooke's Law\";\n",
    "end Spring;\n",
    "```\n",
    "\n",
    "\n",
    "### 阻尼器模型\n",
    "\n",
    "同样，Damper模型也类似地简化为：\n",
    "\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.Components;\n",
    "model Damper \"A rotational damper\"\n",
    "  parameter Modelica.SIunits.RotationalDampingConstant d;\n",
    "  extends ModelicaByExample.Components.Rotational.Interfaces.Compliant;\n",
    "equation\n",
    "  tau = d*der(phi_rel) \"Damping relationship\";\n",
    "end Damper;\n",
    "```\n",
    "\n",
    "\n",
    "### 机械地面\n",
    "\n",
    "最后，我们可以完成双弹簧质量阻尼系统里唯一剩余的模型了。机械地面模型定义如下：\n",
    "\n",
    "\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.Components;\n",
    "model Damper \"A rotational damper\"\n",
    "  parameter Modelica.SIunits.RotationalDampingConstant d;\n",
    "  extends ModelicaByExample.Components.Rotational.Interfaces.Compliant;\n",
    "equation\n",
    "  tau = d*der(phi_rel) \"Damping relationship\";\n",
    "  annotation (Icon(graphics={\n",
    "```\n",
    "\n",
    "\n",
    "### 双弹簧质量阻尼系统\n",
    "\n",
    "最后，我们已经得到了重建第一章内例子所需的所有部件。使用本节中定义的各个组件，我们基于组件的系统模型其Modelica代码如下：\n",
    "\n",
    "\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.Rotational.Examples;\n",
    "model SMD\n",
    "  Components.Ground ground annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}},\n",
    "        rotation=90,\n",
    "        origin={76,0})));\n",
    "  Components.Damper damper2(d=1)\n",
    "    annotation (Placement(transformation(extent={{30,10},{50,30}})));\n",
    "  Components.Spring spring2(c=5)\n",
    "    annotation (Placement(transformation(extent={{28,-30},{48,-10}})));\n",
    "  Components.Inertia inertia2(\n",
    "    J=1,\n",
    "    phi(fixed=true, start=1),\n",
    "    w(fixed=true, start=0))\n",
    "    annotation (Placement(transformation(extent={{-10,-10},{10,10}})));\n",
    "  Components.Damper damper1(d=0.2)\n",
    "    annotation (Placement(transformation(extent={{-50,10},{-30,30}})));\n",
    "  Components.Spring spring1(c=11)\n",
    "    annotation (Placement(transformation(extent={{-50,-30},{-30,-10}})));\n",
    "  Components.Inertia inertia1(\n",
    "    J=0.4,\n",
    "    phi(fixed=true, start=0),\n",
    "    w(fixed=true, start=0))\n",
    "          annotation (Placement(transformation(extent={{-90,-10},{-70,10}})));\n",
    "equation\n",
    "  connect(ground.flange_a, damper2.flange_b) annotation (Line(\n",
    "      points={{70,0},{66,0},{66,0},{60,0},{60,20},{50,20}},\n",
    "      color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "\n",
    "  connect(ground.flange_a, spring2.flange_b) annotation (Line(\n",
    "      points={{70,0},{60,0},{60,-20},{48,-20}},\n",
    "      color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(damper2.flange_a, inertia2.flange_b) annotation (Line(\n",
    "      points={{30,20},{20,20},{20,0},{10,0}},\n",
    "      color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(spring2.flange_a, inertia2.flange_b) annotation (Line(\n",
    "      points={{28,-20},{20,-20},{20,0},{10,0}},\n",
    "      color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(inertia2.flange_a, damper1.flange_b) annotation (Line(\n",
    "      points={{-10,0},{-20,0},{-20,20},{-30,20}},\n",
    "      color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(inertia2.flange_a, spring1.flange_b) annotation (Line(\n",
    "      points={{-10,0},{-20,0},{-20,-20},{-30,-20}},\n",
    "      color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(damper1.flange_a, inertia1.flange_b) annotation (Line(\n",
    "      points={{-50,20},{-60,20},{-60,0},{-70,0}},\n",
    "      color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  connect(spring1.flange_a, inertia1.flange_b) annotation (Line(\n",
    "      points={{-50,-20},{-60,-20},{-60,0},{-70,0}},\n",
    "      color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "end SMD;\n",
    "```\n",
    "\n",
    "\n",
    "模型的简图如下所示\n",
    "\n",
    "\n",
    "<img src=\"./figure7\\13.png\">\n",
    "\n",
    "\n",
    "<img src=\"./figure7\\14.png\">"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
