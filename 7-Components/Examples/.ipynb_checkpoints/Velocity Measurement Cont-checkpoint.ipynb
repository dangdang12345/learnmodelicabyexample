{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 再探速度测量\n",
    "\n",
    "回想一下我们前面对速度的测量的讨论。讨论发生在我们引入构建可重用组件方法之前。这样一来，为不同的速度测量技术相关添加方程颇为麻烦。\n",
    "\n",
    "本节中，我们将重新审视该主题，并再次讨论所有不同的速度估测技术。和之前一样，我们假设“受控对象”模型（我们希望进行测速的系统）已经存在。但这一次，我们将为不同的速度估测算法创建可重用的组件模型，并将其以图形组件形式添加到受控模型里。\n",
    "\n",
    "## 受控对象模型\n",
    "\n",
    "我们首先处理“受控对象模型”。模型行为和在此前讨论速度的测量时的版本一致。系统示意图如下：\n",
    "\n",
    "<img src=\"./figure7\\Plant1.svg\" >\n",
    "\n",
    "\n",
    "相应的Modelica模型源代码为：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.SpeedMeasurement.Examples;\n",
    "model Plant \"The basic plant model\"\n",
    "  extends ModelicaByExample.Components.Rotational.Examples.SMD;\n",
    "  Components.IdealSensor idealSensor\n",
    "    annotation (Placement(transformation(extent={{-10,-10},{10,10}},\n",
    "                          rotation=90, origin={-20,30})));\n",
    "equation\n",
    "  connect(idealSensor.flange, inertia1.flange_a) annotation (Line(\n",
    "      points={{-20,20},{-20,0},{-10,0}}, color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  annotation(experiment(StopTime=10, Tolerance=1e-006));\n",
    "end Plant;\n",
    "```\n",
    "\n",
    "在本节剩余部分，我们将创建使用不同估算方法的模型，对受控对象模型的inertia1组件进行测速。\n",
    "我们在观察不同的速度近似方法前，让我们先看看受控对象模型的实际响应速度。\n",
    "\n",
    "<img src=\"./figure7\\PBase.png\" >\n",
    "\n",
    "请注意，该响应与我们首次讨论此话题时的一模一样。\n",
    "\n",
    "## 取样保持传感器\n",
    "\n",
    "此前，我们讨论了取样保持的测速方法。在这里，我们将再次讨论这个话题。不过，我们会把速度估测封装为可重用的传感器模型。下面模型实现了速度测量的“采样保持”近似：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.SpeedMeasurement.Components;\n",
    "model SampleHold \"A sample-hold ideal speed sensor\"\n",
    "  extends Interfaces.SpeedSensor;\n",
    "  parameter Modelica.SIunits.Time sample_rate;\n",
    "initial equation\n",
    "  w = der(flange.phi);\n",
    "equation\n",
    "  when sample(0, sample_rate) then\n",
    "    w = der(flange.phi);\n",
    "  end when;\n",
    "end SampleHold;\n",
    "```\n",
    "\n",
    "在行为上，这里的估测方法和我们此前的取样保持实现之间并没有差异。但是，我们这里的方法有所不同。不同在于，我们将估测技术放入可重用的组件模型里。\n",
    "\n",
    "我们再次用技巧免去一些麻烦。这里，我们通过利用partial模型来表示各种传感器模型内的通用代码。我们可以从SpeedSensor模型的定义里发现：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.SpeedMeasurement.Interfaces;\n",
    "partial model SpeedSensor \"The base class for all of our sensor models\"\n",
    "  extends Modelica.Mechanics.Rotational.Interfaces.PartialAbsoluteSensor;\n",
    "  Modelica.Blocks.Interfaces.RealOutput w \"Sensed speed\"\n",
    "    annotation (Placement(transformation(extent={{100,-10},{120,10}})));\n",
    "end SpeedSensor;\n",
    "```\n",
    "\n",
    "我们可以从SpeedSensor模型中看到，输出信号命名为w。但是，我们也看到SpeedSensor是继承自Modelica标准库的另一个模型PartialAbsoluteSensor。PartialAbsoluteSensor模型定义为：\n",
    "\n",
    "```Modelica\n",
    "partial model PartialAbsoluteSensor\n",
    "  \"Partial model to measure a single absolute flange variable\"\n",
    "  extends Modelica.Icons.RotationalSensor;\n",
    "\n",
    "  Flange_a flange \"Flange from which speed will be measured\"\n",
    "    annotation(Placement(transformation(extent={{-110,-10},{-90,10}}, rotation=0)));\n",
    "equation\n",
    "  0 = flange.tau;\n",
    "end PartialAbsoluteSensor;\n",
    "```\n",
    "除了提供一个很好的图标外，PartialAbsoluteSensor模型还带有旋转连接器flange。此外，该模型假定传感器模型完全是被动的（即传感器对其所测系统无影响）。因此，模型对连接点施加零转矩。\n",
    "\n",
    "为了测试这个模型，我们只是从Plant模型进行扩展，加上SampleHold传感器的实例，并将传感器连接到inertia上，如：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.SpeedMeasurement.Examples;\n",
    "model PlantWithSampleHold \"Comparison between ideal and sample-hold sensor\"\n",
    "  extends Plant;\n",
    "  Components.SampleHold sampleHold(sample_rate=0.125)\n",
    "    annotation (Placement(transformation(extent={{-10,-10},{10,10}},\n",
    "                          rotation=90, origin={20,30})));\n",
    "equation\n",
    "  connect(sampleHold.flange, inertia1.flange_b) annotation (Line(\n",
    "      points={{20,20},{20,0},{10,0}}, color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  annotation (experiment(StopTime=10, Tolerance=1e-006));\n",
    "end PlantWithSampleHold;\n",
    "```\n",
    "\n",
    "组装后，我们最终的系统看起来如下：\n",
    "\n",
    "<img src=\"./figure7\\PlantWithSampleHold.svg\" >\n",
    "\n",
    "如果我们对此系统仿真5秒钟，我们可以比较传感器返回的信号与惯性元件的实际速度：\n",
    "\n",
    "<img src=\"./figure7\\PwSH.png\" >\n",
    "\n",
    "这些结果与我们此前在对取样保持方法的讨论里的相一致。\n",
    "\n",
    "## 间隔测量\n",
    "\n",
    "现在让我们转而关注间隔测量方法。同样，我们会从Sensor模型扩展创建出一个可重用的组件模型。这次，传感器实现将使用齿间的时间来估算速度：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.SpeedMeasurement.Components;\n",
    "model IntervalMeasure\n",
    "  \"Estimate speed by measuring interval between encoder teeth\"\n",
    "  extends Interfaces.SpeedSensor;\n",
    "  parameter Integer teeth;\n",
    "  Real next_phi, prev_phi;\n",
    "  Real last_time;\n",
    "protected\n",
    "  parameter Modelica.SIunits.Angle tooth_angle=2*Modelica.Constants.pi/teeth;\n",
    "initial equation\n",
    "  next_phi = flange.phi+tooth_angle;\n",
    "  prev_phi = flange.phi-tooth_angle;\n",
    "  last_time = time;\n",
    "equation\n",
    "  when {flange.phi>=pre(next_phi),flange.phi<=pre(prev_phi)} then\n",
    "    w = tooth_angle/(time-pre(last_time));\n",
    "    next_phi = flange.phi+tooth_angle;\n",
    "    prev_phi = flange.phi-tooth_angle;\n",
    "    last_time = time;\n",
    "  end when;\n",
    "end IntervalMeasure;\n",
    "```\n",
    "\n",
    "向受控对象模型添加上述传感器很容易。只要创建以下的Modelica模型：\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.SpeedMeasurement.Examples;\n",
    "model PlantWithIntervalMeasure\n",
    "  \"Comparison between ideal and an interval measuring sensor\"\n",
    "  extends Plant;\n",
    "  Components.IntervalMeasure intervalMeasure(teeth=200)\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}}, rotation=90,\n",
    "        origin={20,30})));\n",
    "equation\n",
    "  connect(intervalMeasure.flange, inertia1.flange_b) annotation (Line(\n",
    "      points={{20,20},{20,0},{10,0}}, color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  annotation (experiment(StopTime=10, Tolerance=1e-006));\n",
    "end PlantWithIntervalMeasure;\n",
    "```\n",
    "\n",
    "组装后，我们的系统看起来如下：\n",
    "\n",
    "<img src=\"./figure7\\PlantWithIntervalMeasure.svg\" >\n",
    "\n",
    "\n",
    "对上述系统进行仿真，我们得到估测速度的如下结果：\n",
    "<img src=\"./figure7\\PwIM.png\" >\n",
    "\n",
    "正如我们此前在对间隔测量方法中的讨论所看到的一样，估测信号的质量随着齿数减少会严重降低。上图使用的传感器每圈有200齿。如果我们绘制轴的转角以及与其相邻的齿角度，我们会看到，轴在不触发测量的前提下不能移动很远：\n",
    "<img src=\"./figure7\\PwIM_gaps.png\" >\n",
    "\n",
    "\n",
    "另一方面，如果将每圈的齿数降到20，我们会得到以下的结果：\n",
    "\n",
    "<img src=\"./figure7\\PwIMf.png\" >\n",
    "\n",
    "绘制轴的转角以及与其相邻的齿角度，我们会看到测量触发的次数变得更少了。其结果是测量的准确度大幅度降低：\n",
    "\n",
    "<img src=\"./figure7\\PwIMf_gaps.png\" >\n",
    "上述结果与此前对间隔测量的讨论内的结果相一致。因此，我们同样可以验证这些面向组件传感器实现的正确性。\n",
    "\n",
    "## 脉冲计数器\n",
    "\n",
    "最后是脉冲计数方法。同样，我们可将上述估测方法展现为扩展自Sensor模型的可重用组件。\n",
    "\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.SpeedMeasurement.Components;\n",
    "model PulseCounter \"Compute speed using pulse counting\"\n",
    "  extends Interfaces.SpeedSensor;\n",
    "  parameter Modelica.SIunits.Time sample_time;\n",
    "  parameter Integer teeth;\n",
    "  Modelica.SIunits.Angle next_phi, prev_phi;\n",
    "  Integer count;\n",
    "protected\n",
    "  parameter Modelica.SIunits.Angle tooth_angle=2*Modelica.Constants.pi/teeth;\n",
    "initial equation\n",
    "  next_phi = flange.phi+tooth_angle;\n",
    "  prev_phi = flange.phi-tooth_angle;\n",
    "  count = 0;\n",
    "algorithm\n",
    "  when {flange.phi>=pre(next_phi), flange.phi<=pre(prev_phi)} then\n",
    "    next_phi := flange.phi + tooth_angle;\n",
    "    prev_phi := flange.phi - tooth_angle;\n",
    "    count := pre(count) + 1;\n",
    "  end when;\n",
    "  when sample(0,sample_time) then\n",
    "    w := pre(count)*tooth_angle/sample_time;\n",
    "    count := 0 \"Another equation for count?\";\n",
    "  end when;\n",
    "end PulseCounter;\n",
    "```\n",
    "然后将其添加到我们的Plant总模型：\n",
    "```Modelica\n",
    "within ModelicaByExample.Components.SpeedMeasurement.Examples;\n",
    "model PlantWithPulseCounter\n",
    "  \"Comparison between ideal and pulse counting sensor\"\n",
    "  extends Plant;\n",
    "  Components.PulseCounter pulseCounter(sample_time=0.125, teeth=200)\n",
    "    annotation (Placement(transformation(\n",
    "        extent={{-10,-10},{10,10}}, rotation=90,\n",
    "        origin={20,30})));\n",
    "equation\n",
    "  connect(pulseCounter.flange, inertia1.flange_b) annotation (Line(\n",
    "      points={{20,20},{20,0},{10,0}}, color={0,0,0},\n",
    "      smooth=Smooth.None));\n",
    "  annotation (experiment(StopTime=10, Tolerance=1e-006));\n",
    "end PlantWithPulseCounter;\n",
    "```\n",
    "\n",
    "对上述系统进行仿真，我们看到结果与此前对脉冲计数的讨论内的结果相一致：\n",
    "生成的系统显示如下：\n",
    "<img src=\"./figure7\\PwPC.png\" >\n",
    "\n",
    "## 结论\n",
    "本书在此前对速度的测量的讨论里非常详细地描述了用于估测旋转轴速度的各种测量技术。本节目的不是为了重温这些讨论。实际上，我们要展示Modelica语言面向组件特性对此前提出的估测技术有着很大的助益。如我们所见，所有这些方法均可以很好地封装在组件模型内。其结果是，要使用这些不同的估测技术现在变得很容易。只需要将这些传感器模型拖放到系统内，并将其连接到要进行测速的旋转元素即可。"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
